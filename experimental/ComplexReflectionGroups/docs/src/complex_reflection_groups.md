```@meta
CurrentModule = Oscar
```

# Complex reflection groups

## Showcase
As a demonstration, we create the exceptional complex reflection group $G_{4}$ in the
notation of Shepard & Todd [ST54](@cite). We choose as *model*—meaning a concrete matrix
group—the unitary model from [LT09](@cite). The base field of this model is
$\mathbb{Q}(i,\omega)$ where $i$ is the imaginary unity and $\omega = \exp(2\pi i/3)$. The
group is generated by two unitary reflections. We display the first generator, verify that
it is indeed a unitary reflection, and then we compute additional data like a root, the
hyperplane, and the non-trivial eigenvalue of this reflection. We have implemented an own
type encapsulating such data of a reflection.

```@juliarepl
julia> W = complex_reflection_group(4, :LT)
Matrix group of degree 2
  over relative number field of degree 2 over number field

julia> base_ring(W)
Relative number field with defining polynomial x^2 + x + 1
  over number field with defining polynomial x^2 + 1
    over rational field

julia> w = gens(W)[1]
[(-1//2*i - 1//2)*ω   (-1//2*i - 1//2)*ω]
[(-1//2*i + 1//2)*ω    (1//2*i - 1//2)*ω]

julia> is_complex_reflection(w)
true

julia> is_unitary(w)
true

julia> b,w_data = is_complex_reflection_with_data(w);

julia> w_data
Complex reflection of order 3 with root (1, (i + 1)*ω + i)

julia> hyperplane(w_data)
[1   (-i - 1)*ω - 1]

julia> eigenvalue(w_data)
-ω - 1
```

One can now run all avaialable matrix group functionality in OSCAR on this group. For 
example one can compute its invariant ring:

```@juliarepl
julia> IR = invariant_ring(W)
Invariant ring
  of matrix group of degree 2 over relative number field

julia> fundamental_invariants(IR)
2-element Vector{MPolyDecRingElem{Hecke.RelSimpleNumFieldElem{AbsSimpleNumFieldElem}, AbstractAlgebra.Generic.MPoly{Hecke.RelSimpleNumFieldElem{AbsSimpleNumFieldElem}}}}:
 x[1]^4 + (-4*ω - 2)*x[1]^2*x[2]^2 + x[2]^4
 x[1]^5*x[2] - x[1]*x[2]^5
```

One feature of our implementation is that we explicitly support *different models*. The
reason is that different models may have different properties. For example, note that in the
above example the first fundamental invariant has a coefficient in which $\omega$ occurrs,
so it is not defined over the rational numbers. It is somewhat surprising and astonishing
that it has been proven by Marin & Michel [MM10](@cite) that there always exists a model
having the property that they have a system of fundamental invariants defined over the
rational numbers. These models are implemented in CHEVIE [Mic15](@cite). We again look at
$G_4$.

```@juliarepl
julia> W = complex_reflection_group(4, :CHEVIE)
Matrix group of degree 2
  over cyclotomic field of order 3

julia> gens(W)
2-element Vector{MatrixGroupElem{AbsSimpleNumFieldElem, AbstractAlgebra.Generic.MatSpaceElem{AbsSimpleNumFieldElem}}}:
 [1 0; 0 z_3]
 [2//3*z_3+1//3 2//3*z_3-2//3; 1//3*z_3-1//3 1//3*z_3+2//3]

julia> IR = invariant_ring(W)
Invariant ring
  of matrix group of degree 2 over cyclotomic field of order 3

julia> fundamental_invariants(IR)
2-element Vector{MPolyDecRingElem{AbsSimpleNumFieldElem, AbstractAlgebra.Generic.MPoly{AbsSimpleNumFieldElem}}}:
 x[1]^4 - 8*x[1]*x[2]^3
 x[1]^6 + 20*x[1]^3*x[2]^3 - 8*x[2]^6

julia> is_unitary(W)
false
```

Note that the CHEVIE model is not unitary anymore, however. There is always a prize to pay
when choosing models. Both the models from [LT09](@cite) and from [MM10](@cite) involve
fractions. The models implemented in [BCP97](@cite) (by Taylor) have the advantage of being
defined over the ring of integers in a cyclotomic field, so they do not involve fractions:

```@juliarepl
julia> W = complex_reflection_group(4, :Magma)
Matrix group of degree 2
  over cyclotomic field of order 3

julia> gens(W)
2-element Vector{MatrixGroupElem{AbsSimpleNumFieldElem, AbstractAlgebra.Generic.MatSpaceElem{AbsSimpleNumFieldElem}}}:
 [z_3 0; -z_3-1 1]
 [1 z_3+1; 0 z_3]
```

This illustrates that having available different models is useful in practice and we
therefore support this in our implementation.

Another feature of our implementation is that we do not just implement matrix models but 

```@juliarepl
julia> W = complex_reflection_group_type([23, (2,1,4)])
Complex reflection group type G23 x G(2,1,4)

julia> order(W)
46080

julia> num_reflections(W)
31

julia> is_primitive(W)
true
```