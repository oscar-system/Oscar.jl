
################################################################################
#
# Orthogonal direct sums and embeddings of orthogonal groups
#
################################################################################

# this function is used whenever A and B are in direct orthogonal sum in a
# bigger torsion module. Let D be this sum. Since A and B are in orthogonal
# direct sum in D, we can embed O(A) and O(B) in O(D).
#
# This function returns D, the embeddings A\to D and B\to D, as well as O(D)
# together with the embeddings O(A) \to O(D) and O(B) \to O(D)
function _sum_with_embeddings_orthogonal_groups(A::TorQuadModule, B::TorQuadModule)
  D = A+B
  AinD = hom(A, D, TorQuadModuleElem[D(lift(a)) for a in gens(A)])
  BinD = hom(B, D, TorQuadModuleElem[D(lift(b)) for b in gens(B)])
  @assert all(AinD(a)*BinD(b) == 0 for a in gens(A), b in gens(B))
  OD = orthogonal_group(D)
  OA = orthogonal_group(A)
  OB = orthogonal_group(B)

  gene = data.(union(AinD.(gens(A)), BinD.(gens(B))))
  geneOAinOD = elem_type(OD)[]
  for f in gens(OA)
    imgf = data.(union(AinD.(f.(gens(A))), BinD.(gens(B))))
    fab = hom(gene, imgf)
    fD = OD(hom(D, D, fab.map))
    push!(geneOAinOD, fD)
  end

  geneOBinOD = elem_type(OD)[]
  for f in gens(OB)
    imgf = data.(union(AinD.(gens(A)), BinD.(f.(gens(B)))))
    fab = hom(gene, imgf)
    fD = OD(hom(D, D, fab.map))
    push!(geneOBinOD, fD)
  end
  OAtoOD = hom(OA, OD, geneOAinOD; check = false)
  OBtoOD = hom(OB, OD, geneOBinOD; check = false)
  return D, AinD, BinD, OD, OAtoOD, OBtoOD
end

# Construct the direct sum D of A and B. Since the images of A and B are in
# orthogonal direct sum, we can embed O(A) in O(D) and O(B) in O(D).
#
# This function returns D together with the injections A \to D and B \to D, as
# well as O(D) with the embeddings O(A) \to O(D) and O(B) \to O(D).
function _direct_sum_with_embeddings_orthogonal_groups(A::TorQuadModule, B::TorQuadModule)
  D, inj = direct_sum(A, B)
  AinD, BinD = inj
  OD = orthogonal_group(D)
  OA = orthogonal_group(A)
  OB = orthogonal_group(B)

  geneOAinOD = elem_type(OD)[]
  for f in gens(OA)
    m = block_diagonal_matrix([matrix(f), identity_matrix(ZZ, ngens(B))])
    fD = OD(hom(D, D, m); check=false)
    push!(geneOAinOD, fD)
  end

  geneOBinOD = elem_type(OD)[]
  for f in gens(OB)
    m = block_diagonal_matrix([identity_matrix(ZZ, ngens(A)), matrix(f)])
    fD = OD(hom(D, D, m); check=false)
    push!(geneOBinOD, fD)
  end
  OAtoOD = hom(OA, OD, geneOAinOD; check = false)
  OBtoOD = hom(OB, OD, geneOBinOD; check = false)
  return D, AinD, BinD, OD, OAtoOD, OBtoOD
end

###############################################################################
#
#  Local tools
#
###############################################################################


# If fq is an isometry of the torsion module q, we compute the kernel of mu(fq)
# restricted to the p-elementary part of q (i.e. the submodule of q generated by
# all the elements of order p)
#
# This object is defined in Algorithm 2 of [BH23], the glue kernel we aim to use
# for gluing lattices in a p-admissible triples are actually submodules of these
# V's.
function _get_V(fq, mu, p)
  q = domain(fq)
  V, Vinq = primary_part(q, p)
  pV, pVinq = sub(q, [q(lift(divexact(order(g), p)*g)) for g in gens(V) if !(order(g)==1)])
  fpV = restrict_endomorphism(fq, pVinq)
  fpV_mu = evaluate(mu, fpV)
  K, _ = kernel(fpV_mu)
  Ktoq = hom(K, q, [q(lift(a)) for a in gens(K)])
  @hassert :ZZLatWithIsom 1 is_injective(Ktoq)
  return K, Ktoq
end

# This is the rho function as defined in Definition 4.8 of BH23.
function _rho_functor(q::TorQuadModule, p, l::Union{Integer, fmpz}; quad::Bool = (p == 2))
  pq, pqtoq = primary_part(q, p)
  pq = rescale(pq, QQ(p)^(l-1))
  Nv = cover(pq)
  N = relations(pq)
  if quad && p == 2
    fr = _is_free(q, p, l)
  else
    fr = false
  end
  mqf = fr ? QQ(2) : QQ(1)
  if l == 0 
    Gl = N
    Gm = intersect(1//p*N, Nv)
    rholN = torsion_quadratic_module(Gl, p*Gm; modulus = QQ(1), modulus_qf = mqf)
  else
    k = l-1
    m = l+1
    Gk = intersect((1//(p^k))*N, Nv)
    Gl = intersect((1//(p^l))*N, Nv)
    Gm = intersect((1//(p^m))*N, Nv)
    B = Gk+p*Gm
    rholN = torsion_quadratic_module(Gl, B; modulus = QQ(1), modulus_qf = mqf)
  end
  return rholN
end

# A finite bilinear module over the 2-adic integers is even if all square are
# zeros.
function _is_even(T, p, l)
  B = gram_matrix_bilinear(_rho_functor(T, p, l; quad = false))
  return is_empty(B) || (all(is_zero, diagonal(B)) && all(is_integral, 2*B))
end

function _is_free(T, p, l)
  return _is_even(T, p, l-1) && _is_even(T, p, l+1)
end

###############################################################################
#
#  Overlattices
#
###############################################################################

# Compute the overlattice corresponding to the glue map gamma, in the ambient
# space of the covering lattice of the finite bilinear module D. As inputs,
# gamma must be an anti-isometry between HA and HB, both should embed in D and
# gamma commutes with the actions of fA and fB on HA and HB respectively.
#
# If `same_ambient = true`, then we consider all the problem in a same ambient
# quadratic space. In particular, the covering lattices of HA, HB and D are all
# in that same space.
#
# fA and fB here are considered as isometry of the relations lattice of HA and
# HB, respectively.
function _overlattice(gamma::TorQuadModuleMor,
                      HAinD::TorQuadModuleMor,
                      HBinD::TorQuadModuleMor,
                      fA::QQMatrix = identity_matrix(QQ, rank(relations(domain(HAinD)))),
                      fB::QQMatrix = identity_matrix(QQ, rank(relations(domain(HBinD))));
                      same_ambient::Bool = false)
  HA = domain(HAinD)
  HB = domain(HBinD)
  A = relations(HA)
  B = relations(HB)
  D = codomain(HAinD)
  if same_ambient
    _glue = Vector{QQFieldElem}[lift(g) + lift(gamma(g)) for g in gens(domain(gamma))]
    z = zero_matrix(QQ, 0, degree(A))
    glue = reduce(vcat, [matrix(QQ, 1, degree(A), g) for g in _glue]; init=z)
    glue = vcat(basis_matrix(A+B), glue)
    glue = FakeFmpqMat(glue)
    _B = hnf(glue)
    _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
    C = lattice(ambient_space(A), _B[end-rank(A)-rank(B)+1:end, :])
    fC = block_diagonal_matrix([fA, fB])
    _B = solve_left(reduce(vcat, basis_matrix.([A,B])), basis_matrix(C))
    fC = _B*fC*inv(_B)
  else
    _glue = Vector{QQFieldElem}[lift(HAinD(a)) + lift(HBinD(gamma(a))) for a in gens(domain(gamma))]
    z = zero_matrix(QQ, 0, degree(cover(D)))
    glue = reduce(vcat, [matrix(QQ, 1, degree(cover(D)), g) for g in _glue]; init=z)
    glue = vcat(block_diagonal_matrix(basis_matrix.([A, B])), glue)
    glue = FakeFmpqMat(glue)
    _B = hnf(glue)
    _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
    C = lattice(ambient_space(cover(D)), _B[end-rank(A)-rank(B)+1:end, :])
    fC = block_diagonal_matrix([fA, fB])
    __B = solve_left(block_diagonal_matrix(basis_matrix.([A, B])), basis_matrix(C))
    fC = __B*fC*inv(__B)
  end
  @hassert :ZZLatWithIsom 1 fC*gram_matrix(C)*transpose(fC) == gram_matrix(C)
  _, graph = sub(D, D.(_glue))
  return C, fC, graph
end

# Same as above where we glue along the trivial subgroups of HA and HB. In that
# particular case, HA and HB are the discriminant group of the lattices
# considered (so HA = L^{\vee}/L for some lattice integral lattice L, and same
# for HB), and D is the orthogonal direct sum of HA and HB in an appropriate
# quadratic space.
function _overlattice(HAinD::TorQuadModuleMor,
                      HBinD::TorQuadModuleMor,
                      fA::QQMatrix = identity_matrix(QQ, rank(relations(domain(HAinD)))),
                      fB::QQMatrix = identity_matrix(QQ, rank(relations(domain(HBinD))));
                      same_ambient::Bool = false)
  HA = domain(HAinD)
  HB = domain(HBinD)
  zA, _ = sub(HA, TorQuadModuleElem[])
  zB, _ = sub(HB, TorQuadModuleElem[])
  gamma = hom(zA, zB, zero_matrix(ZZ, 0, 0))
  return _overlattice(gamma, HAinD, HBinD, fA, fB; same_ambient = same_ambient)
end

##############################################################################
#
#  Orbits and stabilizers of discriminant subgroups
#
##############################################################################

function _on_subgroup_automorphic(T::TorQuadModule, g::AutomorphismGroupElem)
  q = domain(parent(g))
  gene = TorQuadModuleElem[g(q(lift(t))) for t in gens(T)]
  return sub(q, gene)[1]
end

# Compute stabilizer of a subgroup of a `TorQuadModule` under the action by
# automorphisms.
function stabilizer(O::AutomorphismGroup{TorQuadModule}, i::TorQuadModuleMor)
  @req domain(O) === codomain(i) "Incompatible arguments"
  q = domain(O)
  N, _ = sub(q, i.(gens(domain(i))))
  stab, _ = stabilizer(O, N, _on_subgroup_automorphic)
  return sub(O, O.([h.X for h in gens(stab)]))
end

function _subgroups_orbit_representatives_and_stabilizers(Vinq::TorQuadModuleMor,
                                                          O::AutomorphismGroup{TorQuadModule},
                                                          order::IntegerUnion = -1,
                                                          f::Union{TorQuadModuleMor, AutomorphismGroupElem{TorQuadModule}} = id_hom(codomain(Vinq)))
  fV = f isa TorQuadModuleMor ? restrict_endomorphism(f, Vinq) : restrict_endomorphism(hom(f), Vinq)
  V = domain(Vinq)
  q = codomain(Vinq)
  if order == -1
    subs = collect(stable_submodules(V, [fV]))
  else
    subs = collect(submodules(V; order = order))
    filter!(s -> is_invariant(fV, s[2]), subs)
  end
  subs = TorQuadModule[s[1] for s in subs]
  m = gset(O, _on_subgroup_automorphic, subs)
  orbs = orbits(m)
  res = Tuple{TorQuadModuleMor, AutomorphismGroup{TorQuadModule}}[]
  for orb in orbs
    rep = representative(orb)
    stab, _ = stabilizer(O, rep, _on_subgroup_automorphic)
    _, rep = sub(q, TorQuadModuleElem[q(lift(g)) for g in gens(rep)])
    push!(res, (rep, stab))
  end
  return res
end

# The underlying abelian groups of H and V are elementary abelian p-groups, f is
# an automorphism of V fixing H, so in particular it acts on the quotient V/H
# whose abelian structure actually defines a finite dimensional Fp-vector space.
#
# This map return Qp := V/H as an Fp-vector space, the map which transforms V into a
# Fp-vector space Vp, the quotient map Vp \to Qp, and the restriction fQp of f
# to Qp
function _cokernel_as_Fp_vector_space(HinV, p)
  H = domain(HinV)
  V = codomain(HinV)

  n = ngens(V)
  F = GF(p)
  Vp = VectorSpace(F, n)

  function _VtoVp(x::TorQuadModuleElem)
    v = data(x).coeff
    return Vp(vec(collect(v)))
  end

  function _VptoV(v::ModuleElem{FpFieldElem})
    x = lift.(v.v)
    return sum([x[i]*V[i] for i in 1:ngens(V)])
  end

  VtoVp = Hecke.MapFromFunc(V, Vp, _VtoVp, _VptoV)
  subgene = elem_type(Vp)[VtoVp(HinV(a)) for a in gens(H)]
  Hp, _ = sub(Vp, subgene)
  Qp, VptoQp = quo(Vp, Hp)

  return Qp, VtoVp, VptoQp
end

# Given an abelian group injection V \to q where the group structure on V is
# abelian p-elementary, compute orbits and stabilizers of subgroups of V of
# order ord, which contains p^l*q and which are fixed under the action of f, under
# the action by automorphisms of q in G. 
#
# Note that V is fixed by the elements in G, f commutes with the elements in G
# and G is seen as a set of outer automorphisms (so two subgroups are in the
# same orbit if they are G-automorphic).
function _subgroups_orbit_representatives_and_stabilizers_elementary(Vinq::TorQuadModuleMor,
                                                                     G::AutomorphismGroup{TorQuadModule},
                                                                     ord::IntegerUnion,
                                                                     f::Union{TorQuadModuleMor, AutomorphismGroupElem{TorQuadModule}} = id_hom(codomain(Vinq)),
                                                                     l::IntegerUnion = -1)
  res = Tuple{TorQuadModuleMor, AutomorphismGroup{TorQuadModule}}[]

  V = domain(Vinq)

  # If V is trivial, then we ignore f and l, we just need to ensure that the
  # order wanted is also 1
  if order(V) == 1
    ord != 1 && (return res)
    push!(res, (Vinq, G))
    return res
  end

  q = codomain(Vinq)
  p = elementary_divisors(V)[1]
  pq, pqtoq = primary_part(q, p)
  l = l < 0 ? valuation(order(pq), p) : l
  g = valuation(ord, p)

  # some other trivial cases: if ord is 1, then l should be null (-1 by default)
  # Otherwise, if ord == order(V), since V is preserved by f and contained the
  # good subgroup of q, we just return V
  if ord == 1
    l < valuation(order(pq), p) && (return res)
    _, triv = sub(codomain(Vinq), TorQuadModuleElem[])
    push!(res, (triv, G))
    return res
  elseif ord == order(V)
    push!(res, (Vinq, G))
    return res
  end

  # In theory, V should contain H0 := p^l*pq where pq is the p-primary part of q
  all(a -> has_preimage(Vinq, (p^l)*pqtoq(a))[1], gens(pq)) || return res
  H0, H0inq = sub(q, [q(lift((p^l)*a)) for a in gens(pq)])
  @hassert :ZZLatWithIsom 1 is_invariant(f, H0inq)

  H0inV = hom(H0, V, [V(lift(a)) for a in gens(H0)])
  @hassert :ZZLatWithIsom 1 is_injective(H0inV)

  # H0 should be contained in the group we want. So either H0 is the only one
  # and we return it, or if order(H0) > ord, there are no subgroups as wanted
  if order(H0) >= ord
    order(H0) > ord && return res
    push!(res, (H0inq, G))
    return res
  end

  # Since V and H0 are elementary p-groups, they can be seen as finite
  # dimensional vector spaces over a finite field, and so is their quotient.
  # Moreover, subgroups of V of order ord and containing H0 are in bijections
  # with cosets in V/H0 of rank val_p(ord-order(H)) over the finite field F_p
  Qp, VtoVp, VptoQp = _cokernel_as_Fp_vector_space(H0inV, p)
  Vp = codomain(VtoVp)

  # Should never happend, but who knows...
  dim(Qp) == 0 && return res

  # We descend G to V for computing stabilizers later on
  GV, GtoGV = restrict_automorphism_group(G, Vinq)
  satV, j = kernel(GtoGV)

  # Automorphisms in G preserved V and H0, since the construction of H0 is
  # natural. Therefore, the action of G descends to the quotient and we look for
  # invariants sub-vector spaces of given rank in the quotient (then lifting
  # generators and putting them with H0 will give us invariant subgroups as
  # wanted)
  act_GV_Vp = FpMatrix[change_base_ring(base_ring(Qp), matrix(gg)) for gg in gens(GV)]
  act_GV_Qp = FpMatrix[solve(VptoQp.matrix, g*VptoQp.matrix) for g in act_GV_Vp]
  MGp = matrix_group(base_ring(Qp), dim(Qp), act_GV_Qp)
  GVtoMGp = hom(GV, MGp, MGp.(act_GV_Qp); check = false)
  GtoMGp = compose(GtoGV, GVtoMGp)

  @hassert :ZZLatWithIsom g-ngens(snf(abelian_group(H0))[1]) < dim(Qp)
  
  F = base_ring(Qp)
  # K is H0 but seen a subvector space of Vp (which is V)
  k, K = kernel(VptoQp.matrix, side = :left)
  gene_H0p = elem_type(Vp)[Vp(vec(collect(K[i,:]))) for i in 1:k]
  orb_and_stab = orbit_representatives_and_stabilizers(MGp, g-k)
  for (orb, stab) in orb_and_stab
    i = orb.map
    gene_orb_in_Qp = elem_type(Qp)[Qp(vec(collect(i(v).v))) for v in gens(domain(i))]
    gene_orb_in_Vp = elem_type(Vp)[preimage(VptoQp, v) for v in gene_orb_in_Qp]

    gene_submod_in_Vp = vcat(gene_orb_in_Vp, gene_H0p)
    gene_submod_in_V = TorQuadModuleElem[preimage(VtoVp, Vp(v)) for v in gene_submod_in_Vp]
    gene_submod_in_q = TorQuadModuleElem[image(Vinq, v) for v in gene_submod_in_V]
    orbq, orbqinq = sub(q, gene_submod_in_q)
    @hassert :ZZLatWithIsom 1 order(orbq) == ord
    # We keep only f-stable subspaces
    is_invariant(f, orbqinq) || continue

    stabq = AutomorphismGroupElem{TorQuadModule}[GtoMGp\(s) for s in gens(stab)]
    stabq, _ = sub(G, union(stabq, gens(satV)))
    # Stabilizers should preserve the actual subspaces, by definition. so if we
    # have lifted since properly, this should hold..
    @hassert :ZZLatWithIsom 1 is_invariant(stabq, orbqinq)
    push!(res, (orbqinq, stabq))
  end
  return res
end

function _classes_automorphic_subgroups(Vinq::TorQuadModuleMor, O::AutomorphismGroup{TorQuadModule}, H::TorQuadModule; f::Union{TorQuadModuleMor, AutomorphismGroupElem{TorQuadModule}} = id_hom(domain(O)))
  if is_elementary_with_prime(H)[1]
    sors = _subgroups_orbit_representatives_and_stabilizers_elementary(Vinq, O, order(H), f)
  else
    sors = _subgroups_orbit_representatives_and_stabilizers(Vinq, O, order(H), f)
  end
  return filter(d -> is_isometric_with_isometry(domain(d[1]), H)[1], sors)
end

#################################################################################
#
# Primitive embeddings for primary lattices
#
#################################################################################

# We compute representatives of isomorphism classes of primitive extensions
# M \oplus N \to L, where we glue along HM and HN which are respectively
# isometric and anti-isometric to H.
#
# We follow the second definition of Nikulin, i.e. we classify up to the action
# of O(N). If `classification == :sublat`, we also classify them up to the
# action of O(M). If `classification == :first`, we return the first embedding
# computed.
function _isomorphism_classes_primitive_extensions(N::ZZLat, M::ZZLat, H::TorQuadModule, classification::Symbol)
  @hassert :ZZLatWithIsom 1 classification in [:first, :emb, :sublat]
  results = Tuple{ZZLat, ZZLat, ZZLat}[]
  prim, p = is_primary_with_prime(H)
  el = prim ? is_elementary(H, p) : false

  qN = discriminant_group(N)
  GN, _ = image_in_Oq(N)

  qM = discriminant_group(M)
  if classification == :emb
    GM = Oscar._orthogonal_group(qM, ZZMatrix[matrix(id_hom(qM))])
  else
    GM, _ = image_in_Oq(M)
  end

  D, inj = direct_sum(qN, qM)
  qNinD, qMinD = inj
  OD = orthogonal_group(D)

  if el
    VN, VNinqN = _get_V(id_hom(qN), minimal_polynomial(identity_matrix(QQ,1)), p)
    VM, VMinqM = _get_V(id_hom(qM), minimal_polynomial(identity_matrix(QQ, 1)), p)
  elseif prim
    VN, VNinqN = primary_part(qN, p)
    VM, VMinqM = primary_part(qM, p)
  else
    VN, VNinqN = qN, id_hom(qN)
    VM, VMinqM = qM, id_hom(qM)
  end

  subsN = _classes_automorphic_subgroups(VNinqN, GN, rescale(H, -1))
  @hassert :ZZLatWithIsom 1 !isempty(subsN)
  subsM = _classes_automorphic_subgroups(VMinqM, GM, H)
  @hassert :ZZLatWithIsom 1 !isempty(subsM)

  for H1 in subsN, H2 in subsM
    ok, phi = is_anti_isometric_with_anti_isometry(domain(H1[1]), domain(H2[1]))
    @hassert :ZZLatWithIsom 1 ok

    HNinqN, stabN = H1
    HNinD = compose(HNinqN, qNinD)
    HN = domain(HNinqN)
    OHN = orthogonal_group(HN)

    HMinqM, stabM = H2
    HMinD = compose(HMinqM, qMinD)
    HM = domain(HMinqM)
    OHM = orthogonal_group(HM)

    actN = hom(stabN, OHN, [OHN(restrict_automorphism(x, HNinqN)) for x in gens(stabN)])
    actM = hom(stabM, OHM, [OHM(restrict_automorphism(x, HMinqM)) for x in gens(stabM)])
    imM, _ = image(actM)

    stabNphi = AutomorphismGroupElem{TorQuadModule}[OHM(compose(inv(phi), compose(hom(actN(g)), phi))) for g in gens(stabN)]
    stabNphi, _ = sub(OHM, stabNphi)

    reps = double_cosets(OHM, stabNphi, imM)
    @vprintln :ZZLatWithIsom 1 "$(length(reps)) isomorphism classe(s) of primitive extensions"

    for g in reps
      g = representative(g)
      phig = compose(phi, hom(g))
      L, _, _ = _overlattice(phig, HNinD, HMinD)
      N2 = lattice_in_same_ambient_space(L, hcat(basis_matrix(N), zero_matrix(QQ, rank(N), degree(M))))
      @hassert  :ZZLatWithIsom 1 genus(N) == genus(N2)
      M2 = lattice_in_same_ambient_space(L, hcat(zero_matrix(QQ, rank(M), degree(N)), basis_matrix(M)))
      @hassert :ZZLatWithIsom 1 genus(M) == genus(M2)
      push!(results, (L, M2, N2))
      @vprintln :ZZLatWithIsom 1 "Gluing done"
      GC.gc()
      classification == :first && return results
    end
  end
  return results
end

@doc raw"""
    primitive_embeddings_in_primary_lattice(L::ZZLat, M::ZZLat;
                                            classification::Symbol = :sublat,
                                            check::Bool = true)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given a $p$-primary lattice `L`, which is unique in its genus, and an integer
lattice `M`, return whether `M` embeds primitively in `L`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in `L`. The second output `V` consists on triples
`(L', M', N')` where `L'` isometric to `L`, `M'` is a primtiive sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: `V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in `L`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant group of `L`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices of `L` isometric to `M` up to the action of $O(q)$ where `q` is the discriminant group of `L`.

If `check` is set to true, the function determines whether `L` is in fact unique
in its genus.

# Examples
We can use such primitive embeddings algorithm to classify embedding in unimodular
lattices

```jldoctest
julia> E8 = root_lattice(:E,8);

julia> A4 = root_lattice(:A,4);

julia> bool, pe = primitive_embeddings_in_primary_lattice(E8, A4)
(true, Tuple{ZZLat, ZZLat, ZZLat}[(Integer lattice of rank 8 and degree 8, Integer lattice of rank 4 and degree 8, Integer lattice of rank 4 and degree 8)])

julia> pe
1-element Vector{Tuple{ZZLat, ZZLat, ZZLat}}:
 (Integer lattice of rank 8 and degree 8, Integer lattice of rank 4 and degree 8, Integer lattice of rank 4 and degree 8)

julia> genus(pe[1][2]) == genus(pe[1][3])
true
```
To be understood here that there exists a unique class of embedding of the root
lattice $A_4$ in the root lattice $E_8$, and the orthogonal primitive sublattice
is isometric to $A_4$.
"""
function primitive_embeddings_in_primary_lattice(L::ZZLat, M::ZZLat; classification::Symbol = :sublat, check::Bool = true)
  if check
    @req length(genus_representatives(L)) == 1 "L must be unique in its genus"
  end
  return primitive_embeddings_in_primary_lattice(genus(L), M; classification = classification)
end

@doc raw"""
    primitive_embeddings_in_primary_lattice(q::TorQuadModule, sign::Tuple{Int, Int}, M::ZZLat;
                                            classification::Symbol = :sublat)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given a tuple `sign` of non-negative integers and a torsion quadratic module
`q` which define a genus symbol `G` for $p$-primary lattices, return whether the
integer lattice `M` embeds primitively in a lattice in `G`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in a lattice in `G`. The second output `V` consists on
triples `(L', M', N')` where `L'` is a lattice in `G`, `M'` is a sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: `V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in lattices in `G`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant group of a lattice in `G`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices of lattices in `G` isometric to `M`, up to the action of $O(q)$ where `q` is the discriminant group of a lattice in `G`.

If the pair `(q, sign)` does not define a non-empty genus for integer lattices,
an error is thrown.
"""
function primitive_embeddings_in_primary_lattice(q::TorQuadModule, sign::Tuple{Int, Int}, M::ZZLat; classification::Symbol = :sublat)
  @req is_genus(q, sign) "Invariants define the empty genus"
  G = genus(q, sign)
  return primitive_embeddings_in_primary_lattice(G, M; classification = classification)
end

@doc raw"""
    primitive_embeddings_in_primary_lattice(G::ZZGenus, M::ZZLat;
                                            classification::Symbol = :sublat)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given a genus symbol `G` for $p$-primary integer lattices and an integer
lattice `M`, return whether `M` embeds primitively in a lattice in `G`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in a lattice in `G`. The second output `V` consists on
triples `(L', M', N')` where `L'` is a lattice in `G`, `M'` is a sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: `V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in lattices in `G`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant of a lattice in `G`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices of lattices in `G` isometric to `M`, up to the action of $O(q)$ where `q` is the discriminant group of a lattice in `G`.
"""
function primitive_embeddings_in_primary_lattice(G::ZZGenus, M::ZZLat; classification::Symbol = :sublat)
  @req classification in [:none, :emb, :sublat, :first] "Wrong symbol for classification"
  pL, _, nL = signature_tuple(G)
  pM, _, nM = signature_tuple(M)
  @req (pL-pM >= 0 && nL-nM >= 0) "Impossible embedding"

  results = Tuple{ZZLat, ZZLat, ZZLat}[]
  if rank(M) == rank(G)
    genus(M) != G && return false, results
    push!(results, (M, M, orthogonal_submodule(M, M)))
    return true, results
  end

  @req rank(M) < rank(G) "The rank of M must be smaller or equal than the one of the lattices in G"

  bool, p = is_primary_with_prime(G)
  @req bool "G must be unimodular or primary"
  el = is_elementary(G, p)

  qM = discriminant_group(M)
  GM, _ = image_in_Oq(M)
  
  qL = discriminant_group(rescale(G, -1))
  GL = orthogonal_group(qL)

  D, inj = direct_sum(qM, qL)
  qMinD, qLinD = inj

  if el
    VM, VMinqM = _get_V(id_hom(qM), minimal_polynomial(identity_matrix(QQ,1)), p)
  else
    VM, VMinqM = primary_part(qM, p)
  end

  for k in divisors(gcd(order(VM), order(qL)))
    @vprintln :ZZLatWithIsom 1 "Glue order: $(k)"

    if el
      subsL = _subgroups_orbit_representatives_and_stabilizers_elementary(id_hom(qL), GL, k)
    else
      subsL = _subgroups_orbit_representatives_and_stabilizers(id_hom(qL), GL, k)
    end

    VMinD = compose(VMinqM, qMinD)
    @vprintln :ZZLatWithIsom 1 "$(length(subsL)) subgroup(s)"
    for H in subsL
      HL = H[1]
      it = submodules(VM; order = order(domain(HL)))
      subsM = TorQuadModuleMor[j[2] for j in it]
      filter!(HM -> is_anti_isometric_with_anti_isometry(domain(HM), domain(HL))[1], subsM)
      isempty(subsM)  && continue

      @vprintln :ZZLatWithIsom 1 "Possible gluings"
      HM = subsM[1]
      ok, phi = is_anti_isometric_with_anti_isometry(domain(HM), domain(HL))
      @hassert :ZZLatWithIsom 1 ok

      _glue = [lift(qMinD(qM(lift((g))))) + lift(qLinD(qL(lift(phi(g))))) for g in gens(domain(HM))]
      ext, _ = sub(D, D.(_glue))
      perp, j = orthogonal_submodule(D, ext)
      disc = torsion_quadratic_module(cover(perp), cover(ext); modulus = modulus_bilinear_form(perp),
                                                               modulus_qf = modulus_quadratic_form(perp))
      disc2 = rescale(disc, -1)
      !is_genus(disc2, (pL-pM, nL-nM))  && continue

      classification == :none && return true, results

      G2 = genus(disc2, (pL-pM, nL-nM))
      @vprintln :ZZLatWithIsom 1 "We can glue: $(G2)"
      Ns = representatives(G2)
      @vprintln :ZZLatWithIsom 1 "$(length(Ns)) possible orthogonal complement(s)"
      Ns = lll.(Ns)

      # We want to compute the subgroup of `qM` which is identified with a
      # subgroup if `disc`. For this, we need to project `disc` into `qM`.
      # Here `qL` is given as $L1/L2$ for some lattice $L2 \subset L1$. We
      # construct an overlattice `S` of $M\perp L2$ inside $Mv\perp L1$ with
      # respect to `phi`, and we have
      # $M\perp L2 \subset S\subset Sv\subset Mv\perpL1$
      #
      # To have the subgroup, we embed `qM` in `D`. This gives a group `TT`
      # which is of the form $T/M\perp L2$ for some overlattice
      # $M\perp L2 \subset T \subset Mv\perp L1$.
      #
      # The upshot is that both $(T\cap S)/(M\perp L2)$ and
      # $(T\cap Sv)/(M\perpL2) lies in the embedding of $qM \to D$. Their
      # quotient is isometric to the quotient of their preimage.
      # The quotient of their preimage is precisely the subgroup of `qM`
      # we look for.
      S, _, _ = _overlattice(phi, compose(HM, VMinD), compose(HL, qLinD))
      TT, _ = sub(D, qMinD.(gens(qM)))
      qM2 = torsion_quadratic_module(intersect(cover(TT), dual(S)),
                                     intersect(cover(TT), S);
                                     modulus = QQ(1),
                                     modulus_qf = QQ(2))

      for N in Ns
        temp = _isomorphism_classes_primitive_extensions(N, M, qM2, classification)
        if !is_empty(temp)
          classification == :first && return true, temp
          append!(results, temp)
        end
        GC.gc()
      end
    end
  end
  return (length(results) > 0), results
end

@doc raw"""
    primitive_embeddings_of_primary_lattice(L::ZZLat, M::ZZLat;
                                            classification::Symbol = :sublat,
                                            check::Bool = true)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given an integer lattice `L`, which is unique in its genus, and a `p`-primary
lattice `M`, return whether `M` embeds primitively in `L`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in `L`. The second output `V` consists on triples
`(L', M', N')` where `L'` isometric to `L`, `M'` is a primtiive sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: `V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in `L`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant group of `L`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices of `L` isometric to `M` up to the action of $O(q)$ where `q` is the discriminant group of `L`.

If `check` is set to true, the function determines whether `L` is in fact unique
in its genus.

# Examples
We can use such primitive extensions algorithm to have orbits of some short
primitive vectors:

```jldoctest
julia> L = root_lattice(:D, 5);

julia> k = integer_lattice(gram=matrix(QQ,1,1,[4]));

julia> bool, sv = primitive_embeddings_of_primary_lattice(L, k);

julia> bool
true

julia> sv
2-element Vector{Tuple{ZZLat, ZZLat, ZZLat}}:
 (Integer lattice of rank 5 and degree 5, Integer lattice of rank 1 and degree 5, Integer lattice of rank 4 and degree 5)
 (Integer lattice of rank 5 and degree 5, Integer lattice of rank 1 and degree 5, Integer lattice of rank 4 and degree 5)
```
"""
function primitive_embeddings_of_primary_lattice(L::ZZLat, M::ZZLat; classification::Symbol = :sublat, check::Bool = true)
  if check
    @req length(genus_representatives(L)) == 1 "L must be unique in its genus"
  end
  return primitive_embeddings_of_primary_lattice(genus(L), M; classification = classification)
end

@doc raw"""
    primitive_embeddings_of_primary_lattice(q::TorQuadModule, sign::Tuple{Int, Int}, M::ZZLat;
                                            classification::Symbol = :sublat)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given a tuple `sign` of non-negative integers and a torsion quadratic module
`q` which define a genus symbol `G` for integer lattices, return whether the
$p$-primary lattice `M` embeds primitively in a lattice in `G`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in a lattice in `G`. The second output `V` consists on
triples `(L', M', N')` where `L'` is a lattice in `G`, `M'` is a sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: `V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in lattices in `G`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant group of a lattice in `G`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices of lattices in `G` isometric to `M`, up to the action of $O(q)$ where `q` is the discriminant group of a lattice in `G`.

If the pair `(q, sign)` does not define a non-empty genus for integer lattices,
an error is thrown.
"""
function primitive_embeddings_of_primary_lattice(q::TorQuadModule, sign::Tuple{Int, Int}, M::ZZLat; classification::Symbol = :sublat)
  @req is_genus(q, sign) "Invariants define the empty genus"
  G = genus(q, sign)
  return primitive_embeddings_of_primary_lattice(G, M; classification = classification)
end

@doc raw"""
    primitive_embeddings_of_primary_lattice(G::ZZGenus, M::ZZLat;
                                            classification::Symbol = :sublat)
                                        -> Bool, Vector{Tuple{ZZLat, ZZLat, ZZLat}}

Given a genus symbol `G` for integer lattices and a `p`-primary lattice `M`,
return whether `M` embeds primitively in a lattice in `G`.

The first input of the function is a boolean `T` stating whether or not `M`
embeds primitively in a lattice in `G`. The second output `V` consists on
triples `(L', M', N')` where `L'` is a lattice in `G`, `M'` is a sublattice of
`L'` isometric to `M`, and `N'` is the orthogonal complement of `M'` in `L'`.

If `T == false`, then `V` will always be the empty list. If `T == true`, then
the content of `V` actually depends on the value of the symbol `classification`.
There are 4 possibilities:
  - `classification = :none`: `V` is the empty list;
  - `classification = :first`: V` consists on the first primitive embedding found;
  - `classification = :sublat`: `V` consists on representatives for all isomorphism classes of primitive embeddings of `M` in lattices in `G`, up to the actions of $\bar{O}(M)$ and $O(q)$ where `q` is the discriminant group of a lattice in `G`;
  - `classification = :emb`: `V` consists on representatives for all isomorphism classes of primitive sublattices in lattices in `G` isometric to `M`, up to the action of $O(q)$ where `q` is the discriminant group of a lattice in `G`.
"""
function primitive_embeddings_of_primary_lattice(G::ZZGenus, M::ZZLat; classification::Symbol = :sublat)
  @req classification in [:none, :first, :emb, :sublat] "Wrong symbol for classification"
  pL, _, nL = signature_tuple(G)
  pM, _, nM = signature_tuple(M)
  @req (pL-pM >= 0 && nL-nM >= 0) "Impossible embedding"

  results = Tuple{ZZLat, ZZLat, ZZLat}[]
  if rank(M) == rank(G)
    genus(M) != G && return false, results
    push!(results, (M, M, orthogonal_submodule(M, M)))
    return true, results
  end

  @req rank(M) < rank(G) "The rank of M must be smaller or equal than the one of the lattices in G"

  bool, p = is_primary_with_prime(M)
  @req bool "M must be unimodular or primary"
  el = is_elementary(M, p)

  qM = discriminant_group(M)
  GM, _ = image_in_Oq(M)

  qL = discriminant_group(rescale(G, -1))
  GL = orthogonal_group(qL)

  D, inj = direct_sum(qM, qL)
  qMinD, qLinD = inj

  if el
    VL, VLinqL = _get_V(id_hom(qL), minimal_polynomial(identity_matrix(QQ,1)), p)
  else
    VL, VLinqL = primary_part(qL, p)
  end

  for k  in divisors(gcd(order(qM), order(VL)))
    @vprintln :ZZLatWithIsom 1 "Glue order: $(k)"

    if el
      subsL = _subgroups_orbit_representatives_and_stabilizers_elementary(VLinqL, GL, k)
    else
      subsL = _subgroups_orbit_representatives_and_stabilizers(VLinqL, GL, k)
    end
    
    @vprintln :ZZLatWithIsom 1 "$(length(subsL)) subgroup(s)"
    for H in subsL
      HL = H[1]
      it = submodules(qM; order = order(domain(HL)))
      subsM = TorQuadModuleMor[j[2] for j in it]
      filter!(HM -> is_anti_isometric_with_anti_isometry(domain(HM), domain(HL))[1], subsM)
      isempty(subsM)  && continue

      @vprintln :ZZLatWithIsom 1 "Possible gluings"
      HM = subsM[1]
      ok, phi = is_anti_isometric_with_anti_isometry(domain(HM), domain(HL))
      @hassert :ZZLatWithIsom 1 ok

      _glue = [lift(qMinD(qM(lift(g)))) + lift(qLinD(qL(lift(phi(g))))) for g in gens(domain(HM))]
      ext, _ = sub(D, D.(_glue))
      perp, j = orthogonal_submodule(D, ext)
      disc = torsion_quadratic_module(cover(perp), cover(ext); modulus = modulus_bilinear_form(perp),
                                                               modulus_qf = modulus_quadratic_form(perp))
      disc2 = rescale(disc, -1)
      !is_genus(disc2, (pL-pM, nL-nM))  && continue

      classification == :none && return true, results

      G2 = genus(disc2, (pL-pM, nL-nM))
      @vprintln :ZZLatWithIsom 1 "We can glue: $(G2)"
      Ns = representatives(G2)
      @vprintln :ZZLatWithIsom 1 "$(length(Ns)) possible orthogonal complement(s)"
      Ns = lll.(Ns)

      # We want to compute the subgroup of `qM` which is identified with a
      # subgroup if `disc`. For this, we need to project `disc` into `qM`.
      # Here `qL` is given as $L1/L2$ for some lattice $L2 \subset L1$. We
      # construct an overlattice `S` of $M\perp L2$ inside $Mv\perp L1$ with
      # respect to `phi`, and we have
      # $M\perp L2 \subset S\subset Sv\subset Mv\perpL1$
      #
      # To have the subgroup, we embed `qM` in `D`. This gives a group `TT`
      # which is of the form $T/M\perp L2$ for some overlattice
      # $M\perp L2 \subset T \subset Mv\perp L1$.
      #
      # The upshot is that both $(T\cap S)/(M\perp L2)$ and
      # $(T\cap Sv)/(M\perpL2) lies in the embedding of $qM \to D$. Their
      # quotient is isometric to the quotient of their preimage.
      # The quotient of their preimage is precisely the subgroup of `qM`
      # we look for.      S, _, _ = _overlattice(phi, compose(HM, qMinD), compose(HL, qLinD))
      S, _, _ = _overlattice(phi, compose(HM, qMinD), compose(HL, qLinD))
      TT, _ = sub(D, qMinD.(gens(qM)))
      qM2 = torsion_quadratic_module(intersect(cover(TT), dual(S)),
                                     intersect(cover(TT), S);
                                     modulus = QQ(1),
                                     modulus_qf = QQ(2))

      for N in Ns
        temp = _isomorphism_classes_primitive_extensions(N, M, qM2, classification)
        if length(temp) > 0
          classification == :first && return true, temp
          append!(results, temp)
        end
        GC.gc()
      end
      GC.gc()
    end
  end
  return (length(results) > 0), results
end

####################################################################################
#
# Admissible equivariant primitive extensions
#
####################################################################################

@doc raw"""
    admissible_equivariant_primitive_extensions(Afa::ZZLatWithIsom,
                                                Bfb::ZZLatWithIsom,
                                                Cfc::ZZLatWithIsom,
                                                p::Integer,
                                                q::Integer = p; check::Bool = true)
                                                     -> Vector{ZZLatWithIsom}

Given a triple of lattices with isometry `(A, fa)`, `(B, fb)` and `(C, fc)` and a
prime number `p`, such that `(A, B, C)` is `p`-admissible, return a set of
representatives of the double coset $G_B\backslash S/G_A$ where:

  - ``G_A`` and ``G_B`` are the respective images of the morphisms $O(A, fa) \to O(q_A, \bar{fa})$ and $O(B, fb) \to O(q_B, \bar{fb})$;
  - ``S`` is the set of all primitive extensions $A \perp B \subseteq C'$ with isometry $fc'$ where $p\cdot C' \subseteq A\perp B$ and such that the type of $(C', fc'^q)$ is equal to the type of `(C, fc)`.

If `check == true` the input triple is checked to a `p`-admissible triple of
integral lattices (with isometry) with `fA` and `fB` having relatively coprime
irreducible minimal polynomials and imposing that `A` and `B` are orthogonal
if `A`, `B` and `C` lie in the same ambient quadratic space.
"""
function admissible_equivariant_primitive_extensions(A::ZZLatWithIsom,
                                                     B::ZZLatWithIsom,
                                                     C::ZZLatWithIsom,
                                                     p::Integer,
                                                     q::Integer = p; check::Bool = true)
  # p and q can be equal, and they will be most of the time
  @req is_prime(p) && is_prime(q) "p and q must be a prime number" 

  # Requirements for [BH23]
  amb = ambient_space(lattice(A)) === ambient_space(lattice(B)) === ambient_space(lattice(C))
  if check 
    @req all(L -> is_integral(L), [A, B, C]) "Underlying lattices must be integral"
    chiA = minimal_polynomial(A)
    chiB = minimal_polynomial(parent(chiA), isometry(B))
    @req gcd(chiA, chiB) == 1 "Minimal irreducible polynomials must be relatively coprime"
    @req is_admissible_triple(A, B, C, p) "Entries, in this order, do not define an admissible triple with respect to p"
    if amb
      G = gram_matrix(ambient_space(C))
      @req iszero(basis_matrix(A)*G*transpose(basis_matrix(B))) "Lattices in same ambient space must be orthogonal"
    end
  end

  results = ZZLatWithIsom[]

  # this is the glue valuation: it is well-defined because the triple in input is admissible
  g = div(valuation(divexact(det(A)*det(B), det(C)), p), 2)

  qA, fqA = discriminant_group(A)
  qB, fqB = discriminant_group(B)
  qC, _ = discriminant_group(C)
  GA = image_centralizer_in_Oq(A)
  @hassert :ZZLatWithIsom 1 fqA in GA
  GB = image_centralizer_in_Oq(B)
  @hassert :ZZLatWithIsom 1 fqB in GB

  # this is where we will perform the glueing
  if amb
    D, qAinD, qBinD, OD, OqAinOD, OqBinOD = _sum_with_embeddings_orthogonal_groups(qA, qB)
  else
    D, qAinD, qBinD, OD, OqAinOD, OqBinOD = _direct_sum_with_embeddings_orthogonal_groups(qA, qB)
  end

  OqA = domain(OqAinOD)
  OqB = domain(OqBinOD)

  # if the glue valuation is zero, then we glue along the trivial group and we don't
  # have much more to do.
  if g == 0
    # Needed to compute the image of the stabilizer of the isometry we construct
    # (in the orthogonal group of the discriminant group of the new lattice).
    geneA = AutomorphismGroupElem{TorQuadModule}[OqAinOD(OqA(a.X)) for a in gens(GA)]
    geneB = AutomorphismGroupElem{TorQuadModule}[OqBinOD(OqB(b.X)) for b in gens(GB)]
    gene = vcat(geneA, geneB)
    GCAB, _ = sub(OD, gene)

    # We compute the overlattice in this context
    C2, fC2, _ = _overlattice(qAinD, qBinD, isometry(A), isometry(B); same_ambient = amb)
    C2fC2 = integer_lattice_with_isometry(C2, fC2; ambient_representation = false)

    qC2 = discriminant_group(C2)
    phi2 = hom(qC2, D, TorQuadModuleElem[D(lift(x)) for x in gens(qC2)])
    @hassert :ZZLatWithIsom 1 is_isometry(phi2)

    # If not of the good type, we discard it
    !is_of_type(C2fC2^q, type(C)) && return results

    # This is the new image of the stabilizer, just a direct product of
    # the previous ones
    GC2 = Oscar._orthogonal_group(qC2, [compose(phi2, compose(hom(g), inv(phi2))).map_ab.map for g in gens(GCAB)])

    # This is mainly to check that we have not done anything inconsistent
    @hassert :ZZLatWithIsom 1 discriminant_group(C2fC2)[2] in GC2
    set_attribute!(C2fC2, :image_centralizer_in_Oq, GC2)
    push!(results, C2fC2)
    return results
  end

  # these are GA|GB-invariant, fA|fB-stable, and should contain the kernels of any glue map.
  # VA and VB are submodules of the p-elementary parts of qA and qB
  # respectively.
  VA, VAinqA = _get_V(hom(fqA), minimal_polynomial(B), p)
  VB, VBinqB = _get_V(hom(fqB), minimal_polynomial(A), p)

  # since the glue kernels must have order p^g, in this condition, we have nothing
  if min(order(VA), order(VB)) < p^g
    return results
  end

  # scale of the dual: any glue kernel must contain the multiples of l of the respective
  # primary part of the discriminant groups
  l = valuation(level(genus(C)), p)

  # In the special case where rho_{l+1}(A) and rho_{l+1}(B) are free, then we
  # know that rho_l(C) is even if and only if any admissible gluing should
  # induce an isometry of finite quadratic form between rho_{l+1}(A) and
  # rho_{l+1}(B) (this works only when `p == 2`). In all the other cases, then
  # admissible gluings only induce isometries of finite bilinear modules between
  # rho_{l+1}(A) and rho_{l+1}(B).
  spec = (p == 2) && (_is_free(qA, p, l+1)) && (_is_free(qB, p, l+1)) && (_is_even(qC, p, l))

  # We look for the GA|GB-invariant and fA|fB-stable subgroups of VA|VB which respectively
  # contained lpqA|lpqB, where pqA and pqB are respectively the p-primary parts of qA and qB.
  # This is done by computing orbits and stabilisers of VA/lpqA (resp VB/lpqB)
  # seen as a F_p-vector space under the action of GA (resp. GB). Then we check which ones
  # are fA-stable (resp. fB-stable)
  subsA = _subgroups_orbit_representatives_and_stabilizers_elementary(VAinqA, GA, p^g, fqA, ZZ(l))
  subsB = _subgroups_orbit_representatives_and_stabilizers_elementary(VBinqB, GB, p^g, fqB, ZZ(l))

  # once we have the potential kernels, we create pairs of anti-isometric groups since glue
  # maps are anti-isometry
  R = Tuple{eltype(subsA), eltype(subsB), TorQuadModuleMor}[]
  for H1 in subsA, H2 in subsB
    ok, phi = is_anti_isometric_with_anti_isometry(domain(H1[1]), domain(H2[1]))
    !ok && continue
    push!(R, (H1, H2, phi))
  end

  # now, for each pair of anti-isometric potential kernels, we need to massage the gluing
  # computed to turn it into an admissible one. Then, we need to decide whether
  # such an admissible gluing can be made (fA,fB)-equivariant, up to conjugacy.
  #
  # Each pair for which we can find such nice gluing, we create the double coset
  # parametrising all such gluing up to certain conditions and we then compute the
  # corresponding overlattice and check whether it satisfies the type conditions.
  for (H1, H2, phi) in R
    SAinqA, stabA = H1
    SA = domain(SAinqA)
    SAinD = compose(SAinqA, qAinD)
    OSA = orthogonal_group(SA)

    SBinqB, stabB = H2
    SB = domain(SBinqB)
    SBinD = compose(SBinqB, qBinD)
    OSB = orthogonal_group(SB)
    
    # we need a first admissible gluing. We know that such gluing exists because
    # we have an admissible triple as input and the glue kernels have been
    # chosen in such a way that their exist an admissible gluing between them.
    phi = _find_admissible_gluing(SAinqA, SBinqB, phi, l, spec)

    # we compute the image of the stabilizers in the respective OS* and we keep track
    # of the elements of the stabilizers acting trivially in the respective S*
    # (there are in the ker*).
    actA = hom(stabA, OSA, [OSA(restrict_automorphism(x, SAinqA)) for x in gens(stabA)])
    imA, _ = image(actA)
    kerA = AutomorphismGroupElem{TorQuadModule}[OqAinOD(x) for x in gens(kernel(actA)[1])]
    push!(kerA, OqAinOD(one(OqA)))
    fSA = OSA(restrict_automorphism(fqA, SAinqA))

    actB = hom(stabB, OSB, [OSB(restrict_automorphism(x, SBinqB)) for x in gens(stabB)])
    imB, _ = image(actB)
    kerB = AutomorphismGroupElem{TorQuadModule}[OqBinOD(x) for x in gens(kernel(actB)[1])]
    push!(kerB, OqBinOD(one(OqB)))
    fSB = OSB(restrict_automorphism(fqB, SBinqB))

    # We want all isometries of SB which preserves p^l*q_B and such that they
    # define isometries of rho_{l+1}(B). If `spec == true`, then rho_{l+1}(B) is
    # equiped with a quadratic form and we check isometries preserving it.
    # Otherwise, only isometries preserving the underlying bilinear product.
    OSBrB = _compute_double_stabilizer(SBinqB, l, spec)
    @hassert :ZZLatWithIsom 1 fSB in OSBrB   # Should always hold since the construction of rho_{l+1}(B) is natural in B
    fSB = OSBrB(fSB)

    # phi might not "send" the restriction of fA to this of fB, but at least phi*fA*phi^-1
    # should be conjugate to fB inside O(SB, rho_l(qB)) for the gluing.
    # If not, we try the next potential pair.
    fSAinOSB = OSB(compose(inv(phi), compose(hom(fSA), phi)))
    @hassert :ZZLatWithIsom 1 fSAinOSB in OSBrB  # Same as before, since phi is admissible, then the image of fSA should preserve rho_{l+1}(B)
    bool, g0 = representative_action(OSBrB, OSBrB(fSAinOSB), fSB)
    bool || continue

    # The new phi is "sending" the restriction of fA to this of fB
    # and it is still admissible. So we can glue SA and SB as wanted.
    phi = compose(phi, hom(OSB(g0)))
    fSAinOSB = OSBrB(compose(inv(phi), compose(hom(fSA), phi)))
    @hassert :ZZLatWithIsom 1 fSAinOSB == fSB

    # Now it is time to compute generators for O(SB, rho_l(qB), fB), and the induced
    # images of stabA|stabB for taking the double cosets next
    center, _ = centralizer(OSBrB, fSB)
    center, _ = sub(OSB, [OSB(c) for c in gens(center)])
    stabSAphi, _ = sub(OSB, AutomorphismGroupElem{TorQuadModule}[OSB(compose(inv(phi), compose(hom(g), phi))) for g in gens(imA)])
    stabSAphi, _ = intersect(center, stabSAphi)
    stabSB, _ = intersect(center, imB)
    reps = double_cosets(center, stabSB, stabSAphi)

    # We iterate over all double cosets. Each representative, define a new
    # classe of admissible gluing and so, for each such representative we compute the
    # corresponding overlattice along the gluing. If it has the wanted type, we compute
    # the image of the centralizer in OD from the stabA and stabB.
    for g in reps
      g = representative(g)
      phig = compose(phi, hom(g))
      @assert is_anti_isometry(phig)

      # We compute the overlattice in this context, keeping track whether we
      # cork in a fixed ambient quadratic space
      C2, fC2, extinD = _overlattice(phig, SAinD, SBinD, isometry(A), isometry(B); same_ambient = amb)
      C2fC2 = integer_lattice_with_isometry(C2, fC2; ambient_representation = false)

      # This is the type requirement: somehow, we want `(C2, fC2)` to be a "q-th root" of `(C, fC)`.
      !is_of_type(C2fC2^q, type(C)) && continue

      # By the theory of primitive extensions, the discriminant group qC2 of C2
      # is equal to H^{perp}/H where H is the graph of phig in D = qA + qB. We need
      # to treat both at the same time to compute the image of the centralizer
      # O(C2, fC2) in O(qC2, fqC2) using GA and GB.
      ext = domain(extinD)
      perp, j = orthogonal_submodule(D, ext)
      disc = torsion_quadratic_module(cover(perp), cover(ext); modulus = modulus_bilinear_form(perp),
                                                               modulus_qf = modulus_quadratic_form(perp))
      qC2 = discriminant_group(C2)
      OqC2 = orthogonal_group(qC2)
      phi2 = hom(qC2, disc, [disc(lift(x)) for x in gens(qC2)])
      @hassert :ZZLatWithIsom 1 is_isometry(phi2)              # In fact they are the same module so phi2, mathematically, is the identity.

      # So now this new integer lattice with isometry `(C2, fC2)` is a good
      # output. Just remain toi compute GC2 in a smart way.
      geneOSA =  AutomorphismGroupElem{TorQuadModule}[OSA(compose(phig, compose(hom(g1), inv(phig)))) for g1 in unique(gens(imB))]
      im2_phi, _ = sub(OSA, geneOSA)
      gene_inter = AutomorphismGroupElem{TorQuadModule}[h for h in unique(gens(intersect(imA, im2_phi)[1]))]
      im3, _ = sub(imA, gene_inter)
      stab = Tuple{AutomorphismGroupElem{TorQuadModule}, AutomorphismGroupElem{TorQuadModule}}[(actA\x, actB\(imB(compose(inv(phig), compose(hom(x), phig))))) for x in gens(im3)]
      stab = AutomorphismGroupElem{TorQuadModule}[OqAinOD(x[1])*OqBinOD(x[2]) for x in stab]
      stab = union(stab, kerA)
      stab = union(stab, kerB)
      stab = TorQuadModuleMor[restrict_automorphism(g, j) for g in stab]
      stab = TorQuadModuleMor[hom(disc, disc, [disc(lift(g(perp(lift(l))))) for l in gens(disc)]) for g in stab]
      stab = Oscar._orthogonal_group(qC2, [compose(phi2, compose(g, inv(phi2))).map_ab.map for g in stab])

      # If we have done good things, the action of fC2 on qC2 should centralize
      # itself...
      @hassert :ZZLatWithIsom 1 discriminant_group(C2fC2)[2] in stab

      set_attribute!(C2fC2, :image_centralizer_in_Oq, stab)
      push!(results, C2fC2)
    end
  end
  return results
end

###############################################################################
#
#  Computation of O(H_B, rho_{l+1}(B))
#  ===================================
#  [BH23, Definition 4.16]
#
###############################################################################


# Action of isometries on the gram matrix of a finite bilinear form
function _on_modular_matrix(M::QQMatrix, g::AutomorphismGroupElem)
  q = domain(parent(g))
  R = Hecke.QmodnZ(QQ(1))
  m = matrix(inv(g))
  return map_entries(a -> lift(R(a)), m*M*transpose(m))
end

# Action of isometries on the gram matrix of a finite quadratic form
function _on_modular_matrix_quad(M::QQMatrix, g::AutomorphismGroupElem)
  q = domain(parent(g))
  R1 = Hecke.QmodnZ(QQ(1))
  R2 = Hecke.QmodnZ(QQ(2))
  m = matrix(inv(g))
  m1 = m*M*transpose(m)
  for i in 1:nrows(m1)
    for j in 1:ncols(m1)
      if i == j
        m1[i,j] = lift(R2(m1[i,j]))
      else
        m1[i,j] = lift(R1(m1[i,j]))
      end
    end
  end
  return m1
end

# We compute O(SB, rho_{l+1}(B)) where B has discriminant form qB. `spec` keep
# track whether rho_{l+1}(B) should be considered as a finite quadratic module
# or just a finite bilinear module (depends on the overlattice).
function _compute_double_stabilizer(SBinqB, l, spec)
  SB = domain(SBinqB)
  qB = codomain(SBinqB)
  OSB = orthogonal_group(SB)
  p = elementary_divisors(SB)[1]
  rB = _rho_functor(qB, p, l+1)
  rBtoSB = hom(rB, SB, [SB(QQ(p^l)*lift(a)) for a in gens(rB)])
  HB, HBinSB = sub(SB, rBtoSB.(gens(rB)))
  OSBHB, _ = stabilizer(OSB, HBinSB)
  OHB, OSBHBtoOHB = restrict_automorphism_group(OSBHB, HBinSB)
  K, _ = kernel(OSBHBtoOHB)
  if spec
    OHBrB, _ = stabilizer(OHB, gram_matrix_quadratic(rB), _on_modular_matrix_quad)
  else
    OHBrB, _ = stabilizer(OHB, gram_matrix_bilinear(rB), _on_modular_matrix)
  end
  OSBrB, _ = sub(OSB, union(gens(K), OSB.(gens((OSBHBtoOHB\(OHBrB))[1]))))
  return OSBrB
end


###############################################################################
#
#  Isometries between finite bilinear modules
#
###############################################################################

# Test whether an abelian group isomorphism respect the finite bilinear product
# of its domain (of type `TorQuadModule`).
function _is_isometry_bilinear(f::TorQuadModuleMor)
  !is_bijective(f) && return false
  for a in gens(domain(f))
    for b in gens(domain(f))
      if f(a)*f(b) != a*b
        return false
      end
    end
  end
  return true
end

# Test whether an abelian group isomorphism defines an anti isometry of the
# finite bilinear product defined on its domain (of type `TorQuadModule`)
function _is_anti_isometry_bilinear(f::TorQuadModuleMor)
  !is_bijective(f) && return false
  for a in gens(domain(f))
    for b in gens(domain(f))
      if f(a)*f(b) != -a*b
        return false
      end
    end
  end
  return true
end

# Compute an anti-isometry between the two finite bilinear modules r1 and r2.
function _anti_isometry_bilinear(r1, r2)
  @hassert :ZZLatWithIsom is_semi_regular(r1) === is_semi_regular(r2) === true
  hz = hom(r1, r2, zero_matrix(ZZ, ngens(r1), ngens(r2)))
  r2m = rescale(r2, -1)
  r2tor2m = hom(r2, r2m, identity_matrix(ZZ, ngens(r2)))
  r1N, r1tor1N = normal_form(r1)
  r2mN, r2mtor2mN = normal_form(r2m)
  gram_matrix_bilinear(r1N) == gram_matrix_bilinear(r2mN) || return false, hz
  T = hom(r1N, r2mN, identity_matrix(ZZ, ngens(r1N)))
  T = compose(r1tor1N, compose(T, compose(inv(r2mtor2mN), inv(r2tor2m))))
  @hassert :ZZLatWithIsom _is_anti_isometry_bilinear(T)
  return true, T
end

###############################################################################
#
#  Admissible gluings
#
###############################################################################

# If we are given a gluing between SA and SB, given that an admissible gluing
# exist, we massage phi until we turn it into an admissible gluing. There might
# be ways to improve such search, but I would expect that both loops iterating
# on OSB and OSBHB are terminating after few tries.
function _find_admissible_gluing(SAinqA, SBinqB, phi, l, spec)
  SA = domain(SAinqA)
  SB = domain(SBinqB)
  p = elementary_divisors(SA)[1]
  qA = codomain(SAinqA)
  qB = codomain(SBinqB)
  pqA, pqAtoqA = primary_part(qA, p)
  pqB, pqBtoqB = primary_part(qB, p)
  rA = _rho_functor(qA, p, l+1)
  rB = _rho_functor(qB, p, l+1)
  rAtoSA = hom(rA, SA, [SA(QQ(p^l)*lift(a)) for a in gens(rA)])
  HA, _ = sub(SA, rAtoSA.(gens(rA)))
  rBtoSB = hom(rB, SB, [SB(QQ(p^l)*(lift(b))) for b in gens(rB)])
  HB, HBinSB = sub(SB, rBtoSB.(gens(rB)))
  if spec
    ok, phi_0 = is_anti_isometric_with_anti_isometry(rA, rB)
    @hassert :ZZLatWithIsom 1 ok
    @hassert :ZZLatWithIsom 1 is_anti_isometry(phi_0)
  else
    ok, phi_0 = _anti_isometry_bilinear(rA, rB)
    @hassert :ZZLatWithIsom 1 ok
    @hassert :ZZLatWithIsom 1 _is_anti_isometry_bilinear(phi_0)
  end
  phiHA, _ = sub(SB, [phi(SA(lift(a))) for a in gens(HA)])
  OSB = orthogonal_group(SB)
  g = one(OSB)
  for f in OSB
    g = f
    if cover(_on_subgroup_automorphic(phiHA, g)) == cover(HB)
      break
    end
  end
  phi_1 = compose(phi, hom(g))
  OSBHB, _ = stabilizer(OSB, HBinSB)
  g = one(OSBHB)
  for f in OSBHB
    g = f
    phif = compose(phi_1, hom(f))
    psi = hom(rA, rB, [rBtoSB\(phif(rAtoSA(a))) for a in gens(rA)])
    if matrix(psi) == matrix(phi_0)
      break
    end
  end
  phig = compose(phi_1, hom(g))
  return phig
end
