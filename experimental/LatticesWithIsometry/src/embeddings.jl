const GG = GAP.Globals

################################################################################
#
# Miscellaneous
#
################################################################################

# this function is used whenever A and B are in direct orthogonal sum in a
# bigger torsion module. Let D be this sum. Since A and B are in orthogonal
# direct sum in D, we can embed O(A) and O(B) in O(D).
#
# This function returns D, the embeddings A\to D and B\to D, as well as O(D)
# together with the embeddings O(A) \to O(D) and O(B) \to O(D)
function _sum_with_embeddings_orthogonal_groups(A::TorQuadModule, B::TorQuadModule)
  D = A+B
  AinD = hom(A, D, TorQuadModuleElem[D(lift(a)) for a in gens(A)])
  BinD = hom(B, D, TorQuadModuleElem[D(lift(b)) for b in gens(B)])
  @assert all(v -> AinD(v[1])*BinD(v[2]) == 0, Hecke.cartesian_product_iterator([gens(A), gens(B)], inplce=true))
  OD = orthogonal_group(D)
  OA = orthogonal_group(A)
  OB = orthogonal_group(B)

  gene = data.(union(AinD.(gens(A), BinD.(gens(B)))))
  geneOAinOD = elem_type(OD)[]
  for f in gens(OA)
    imgf = data.(union(AinD.(f.(gens(A))), BinD.(gens(B))))
    fab = hom(gene, imgf)
    fD = OD(hom(D, D, fab.map))
    push!(geneOAinOD, fD)
  end

  geneOBinOD = elem_type(OD)[]
  for f in gens(OB)
    imgf = data.(union(AinD.(gens(A)), BinD.(f.(gens(B)))))
    fab = hom(gene, imgf)
    fD = OD(hom(D, D, fab.map))
    push!(geneOBinOD, fD)
  end
  OAtoOD = hom(OA, OD, geneOAinOD, check = false)
  OBtoOD = hom(OB, OD, geneOBinOD, check = false)
  return D, AinD, BinD, OD, OAtoOD, OBtoOD
end

# Construct the direct sum D of A and B. Since the images of A and B are in
# orthogonal direct sum, we can embed O(A) in O(D) and O(B) in O(D).
#
# This function returns D together with the injections A \to D and B \to D, as
# well as O(D) with the embeddings O(A) \to O(D) and O(B) \to O(D).
function _direct_sum_with_embeddings_orthogonal_groups(A::TorQuadModule, B::TorQuadModule)
  D, inj = direct_sum(A, B)
  AinD, BinD = inj
  OD = orthogonal_group(D)
  OA = orthogonal_group(A)
  OB = orthogonal_group(B)

  geneOAinOD = elem_type(OD)[]
  for f in gens(OA)
    m = block_diagonal_matrix([matrix(f), identity_matrix(ZZ, ngens(B))])
    fD = OD(hom(D, D, m), check=false)
    push!(geneOAinOD, fD)
  end

  geneOBinOD = elem_type(OD)[]
  for f in gens(OB)
    m = block_diagonal_matrix([identity_matrix(ZZ, ngens(A)), matrix(f)])
    fD = OD(hom(D, D, m), check=false)
    push!(geneOBinOD, fD)
  end
  OAtoOD = hom(OA, OD, geneOAinOD, check = false)
  OBtoOD = hom(OB, OD, geneOBinOD, check = false)
  return D, AinD, BinD, OD, OAtoOD, OBtoOD
end

# If fq is an isometry of the torsion module q, we compute the kernel of mu(fq)
# restricted to the p-elementary part of q (i.e. the submodule of q generated by
# all the elements of order p)
#
# This object is defined in Algorithm 2 of BH23, the glue kernel we aim to use
# for gluing lattices in a p-admissible triples are actually submodules of these
# V's.
function _get_V(fq, mu, p)
  q = domain(fq)
  V, Vinq = primary_part(q, p)
  pV, pVinq = sub(q, Vinq.([divexact(order(g), p)*g for g in gens(V) if !(order(g)==1)]))
  fpV = restrict_automorphism(fq, pVinq)
  fpV_mu = evaluate(mu, fpV)
  K, KtopV = kernel(fpV_mu)
  Ktoq = compose(KtopV, pVinq)
  fK = restrict_automorphism(fq, Ktoq)
  return K, Ktoq, fK
end

# This is the rho function as defined in Definition 4.8 of BH23.
function _rho_functor(q::TorQuadModule, p, l::Union{Integer, fmpz})
  N = relations(q)
  if l == 0 
    Gl = N
    Gm = intersect(1//p*N, dual(N))
    rholN = torsion_quadratic_module(Gl, p*Gm, modulus = QQ(1), modulus_qf = QQ(2))
    gene = elem_type(q)[q(lift(g)) for g in gens(rholN)]
    return sub(q, gene)
  end  
  k = l-1
  m = l+1
  Gk = intersect(1//p^k*N, dual(N))
  Gl = intersect(1//p^l*N, dual(N))
  Gm = intersect(1//p^m*N, dual(N))
  rholN = torsion_quadratic_module(Gl, Gk+p*Gm, modulus = QQ(1), modulus_qf = QQ(2))
  gene = elem_type(q)[q(lift(g)) for g in gens(rholN)]
  return sub(q, gene)
end

##############################################################################
#
#  Orbits and stabilizers of discriminant subgroups
#
##############################################################################

function _on_subgroup_automorphic(H::Oscar.GAPGroup, g::AutomorphismGroupElem)
  G = domain(parent(g))
  return sub(G, g.(gens(H)))[1]
end

function stabilizer(O::AutomorphismGroup{TorQuadModule}, i::TorQuadModuleMor)
  @req domain(O) === codomain(i) "Incompatible arguments"
  q = domain(O)
  togap = get_attribute(O, :to_gap)
  tooscar = get_attribute(O, :to_oscar)
  A = codomain(togap)
  OA = automorphism_group(A)
  OinOA, _ = sub(OA, OA.([g.X for g in gens(O)]))
  N, _ = sub(A, togap.(i.(gens(domain(i)))))
  stab, _ = stabilizer(OinOA, N, _on_subgroup_automorphic)
  return sub(O, O.([h.X for h in gens(stab)]))
end

# The underlying abelian groups of H and V are elementary abelian p-groups, f is
# an automorphism of V fixing H, so in particular it acts on the quotient V/H
# whose abelian structure actually defines a finite dimensional Fp-vector space.
#
# This map return Qp := V/H as an Fp-vector space, the map which transforms V into a
# Fp-vector space Vp, the quotient map Vp \to Qp, and the restriction fQp of f
# to Qp
function _cokernel_as_Fp_vector_space(HinV, p, f)
  if f isa AutomorphismGroupElem
    f = hom(f)
  end
  H = domain(HinV)
  HS, HStoH = snf(H)
  HStoV = compose(HStoH, HinV)
  V = codomain(HinV)
  VS, VStoV = snf(V)

  VtoVS = inv(VStoV)

  n = ngens(Vs)
  F = GF(p)
  MVS = matrix(compose(VstoV, compose(f, VtoVS)))
  Vp = VectorSpace(F, n)

  function _VstoVp(x::TorQuadModuleElem)
    v = data(x).coeff
    return Vp(vec(collect(v)))
  end

  function _VptoVs(v::ModuleElem{FpFieldElem})
    x = lift.(v.v)
    return VS(vec(collect(x)))
  end

  VStoVp = Hecke.MapFromFunc(_VstoVp, _VptoVs, Vs, Vp)
  VtoVp = compose(VtoVs, VStoVp)
  subgene = elem_type(Vp)[VtoVp(HStoV(a)) for a in gens(HS)]
  Hp, _ = sub(Vp, subgene)
  Qp, VptoQp = quo(Vp, Hp)
  fVp = change_base_ring(F, MVs)
  ok, fQp = can_solve_with_solution(VptoQp.matrix, fVp*VptoQp.matrix)
  @assert ok


  return Qp, VtoVp, VptoQp, fQp
end

# Given an abelian group injection V \to q where the group structure on V is
# abelian p-elementary, compute orbits and stabilizers of subgroups of V of
# order ord, which contains l*q and which are fixed under the action of f, under
# the action by automorphisms of q in G. 
#
# Note that V is fixed by the elements in G, f commutes with the elements in G
# and G is seen as a set of outer automorphisms (so two subgroups are in the
# same orbit if they are G-automorphic).
function _subgroups_orbit_representatives_and_stabilizers_elementary(Vinq::TorQuadModuleMor,
                                                                     G::AutomorphismGroup{TorQuadModule},
                                                                     ord::Hecke.IntegerUnion,
                                                                     f::Union{TorQuadModuleMor, AutomorphismGroupElem{TorQuadModule}} = id_hom(domain(Vinq)),
                                                                     l::Hecke.IntegerUnion = 0)
  res = Tuple{TorQuadModuleMor, AutomorphismGroup{TorQuadModule}}[]

  if ord == 1
    l != 0 && (return res)
    _, triv = sub(codomain(Vinq), TorQuadModuleElem[])
    push!(res, (triv, G))
    return res
  end

  V = domain(Vinq)
  q = codomain(Vinq)
  p = elementary_divisors(V)[1]
  pq, pqtoq = primary_part(q, p)
  g = valuation(ord, p)
  @hassert :LatWithIsom 1 all(a -> haspreimage(Vinq, (p^l)*pqtoq(a))[1], gens(pq))
  H0, H0inV = sub(V, [preimage(Vinq, (p^l)*pqtoq(a)) for a in gens(pq)])
  Qp, VtoVp, VptoQp, fQp = _cokernel_as_Fp_vector_space(H0inV, p, f)
  Vp = codomain(VtoVp)

  if dim(Qp) == 0
    order(V) != ord && (return res)
    push!(res, (Vinq, G))
    return res
  end

  OV = orthogonal_group(V)
  GV, GtoGV = restrict_automorphism_group(G, Vinq)

  act_GV_Vp = FpMatrix[change_base_ring(base_ring(Qp), matrix(gg)) for gg in gens(GV)]
  act_GV_Qp = FpMatrix[solve(VptoQp.matrix, g*VptoQp.matrix) for g in act_GV_Vp]
  MGp = matrix_group(act_GV_Qp)
  @hassert :LatWithIsom 1 fQp in MGp
  MGptoGV = hom(MGp, GV, gens(GV), check = false)

  if g-ngens(snf(abelian_group(H0))[1]) > dim(Qp)
    return res
  end

  F = base_ring(Qp)
  k, K = kernel(VptoQp.matrix, side = :left)
  gene_H0p = elem_type(Vp)[Vp(vec(collect(K[i,:]))) for i in 1:k]
  orb_and_stab = orbit_representatives_and_stabilizers(MGp, g-k)
  for (orb, stab) in orb_and_stab
    i = orb.map
    _V = codomain(i)
    for v in gens(orb)
      vv = _V(i(v)*fQp)
      if !can_solve(i.matrix, vv.v, side = :left)
        @goto non_fixed
      end
    end

    gene_orb_in_Qp = elem_type(Qp)[Qp(vec(collect(i(v).v))) for v in gens(orb)]
    gene_orb_in_Vp = elem_type(Vp)[preimage(VptoQp, v) for v in gene_orb_in_Qp]

    gene_submod_in_Vp = vcat(gene_orb_in_Vp, gene_H0p)
    gene_submod_in_V = TorQuadModuleElem[preimage(VtoVp, Vp(v)) for v in gene_submod_in_Vp]
    gene_submod_in_q = TorQuadModuleElem[image(Vinq, v) for v in gene_submod_in_V]
    orbq, orbqinq = sub(q, gene_submod_in_q)
    @hassert :LatWithIsom 1 order(orbq) == ord

    stabq = AutomorphismGroup{TorQuadModule}[GtoGV\(MGptoGV(s)) for s in gens(stab)]
    stabq, _ = sub(G, stabq)
    push!(res, (orbqinq, stabq))
    @label non_fixed
  end
  return res
end

function _subgroups_orbit_representatives_and_stabilizers(O::AutomorphismGroup{TorQuadModule}; order::Hecke.IntegerUnion = -1)
  togap = get_attribute(O, :to_gap)
  tooscar = get_attribute(O, :to_oscar)
  q = domain(O)
  A = abelian_group(q)
  it = order == -1 ? subgroups(A) : subgroups(A, order=order)
  Agap = codomain(togap)
  coAgap = [sub(Agap, togap.(q.(j[2].(gens(j[1])))))[1] for j in it]
  OAgap = automorphism_group(Agap)
  OinOAgap, j = sub(OAgap, OAgap.([g.X for g in gens(O)]))
  m = gset(OinOAgap, on_subgroup, coAgap)
  orbs = orbits(m)
  res = Tuple{TorQuadModuleMor, AutomorphismGroup{TorQuadModule}}[]
  for orb in orbs
    rep = representative(orb)
    stab, _ = stabilizer(OinOAgap, rep, on_subgroup)
    _, rep = sub(q, TorQuadModuleElem[tooscar(Agap(g)) for g in gens(rep)])
    stab, _ = sub(O, O.([h.X for h in gens(stab)]))
    push!(res, (rep, stab))
  end 
  return res
end

function _classes_automorphic_subgroups(O::AutomorphismGroup{TorQuadModule}, q::TorQuadModule)                             
  sors = _subgroups_orbit_representatives_and_stabilizers(O, order=order(q))
  return filter(d -> is_isometric_with_isometry(domain(d[1]), q)[1], sors)
end

#################################################################################
#
# Embeddings in elementary lattices
#
#################################################################################

# here for convenience, we choose in entry N and M to be of full rank and
# with basis matrix equal to the identity matrix
# 
# We compute representatives of isomorphism classes of primitive extensions
# M \oplus N \to L, where we glue along HM and HN which are respectively
# isometric and anti-isometric to H.
#
# We follow the second definition of Nikulin, i.e. we classify up to the actions
# of O(M) and O(N).
function _isomorphism_classes_primitive_extensions(N::ZLat, M::ZLat, H::TorQuadModule)
  @hassert :LatWithIsom 1 is_one(basis_matrix(N))
  @hassert :LatWithIsom 1 is_one(basis_matrix(M))
  results = Tuple{ZLat, ZLat, ZLat}[]
  GN, _ = image_in_Oq(N)
  GM, _ = image_in_Oq(M)
  qN = domain(GN)
  qM = domain(GM)

  D, inj = direct_sum(qN, qM)
  qNinD, qMinD = inj
  OD = orthogonal_group(D)

  subsN = _classes_automorphic_subgroups(GN, rescale(H, -1))
  @hassert :LatWithIsom 1 !isempty(subsN)
  subsM = _classes_automorphic_subgroups(GM, H)
  @hassert :LatWithIsom 1 !isempty(subsM)

  for H1 in subsN, H2 in subsM
    ok, phi = is_anti_isometric_with_anti_isometry(domain(H1[1]), domain(H2[1]))
    @hassert :LatWithIsom 1 ok

    HNinqN, stabN = H1
    HN = domain(HNinqN)
    OHN = orthogonal_group(HN)

    HMinqM, stabM = H2
    HM = domain(HMinqM)
    OHM = orthogonal_group(HM)

    actN = hom(stabN, OHN, [OHN(restrict_automorphism(x, HNinqN)) for x in gens(stabN)])

    actM = hom(stabM, OHM, [OHM(restrict_automorphism(x, HMinqM)) for x in gens(stabM)])
    imM, _ = image(actM)

    stabNphi = AutomorphismGroupElem{TorQuadModule}[OHM(compose(inv(phi), compose(hom(actN(g)), phi))) for g in gens(stabN)]
    stabNphi, _ = sub(OHM, stabNphi)
    reps = double_cosets(OHM, stabNphi, imM)
    @vprint :LatWithIsom 1 "$(length(reps)) isomorphism classe(s) of primitive extensions"

    for g in reps
      g = representative(g)
      phig = compose(phi, hom(g))
      _glue = Vector{QQFieldElem}[lift(qNinD(HNinqN(g))) + lift(qMinD(HMinqM(phig(g)))) for g in gens(domain(phig))]
      z = zero_matrix(QQ, 0, degree(N)+degree(M))
      glue = reduce(vcat, [matrix(QQ, 1, degree(N)+degree(M), g) for g in _glue], init = z)
      glue = vcat(identity_matrix(QQ, rank(N)+rank(M)), glue)
      glue = FakeFmpqMat(glue)
      _B = hnf(glue)
      _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
      L = Hecke.lattice(ambient_space(cover(D)), _B[end-rank(N)-rank(M)+1:end, :])
      N2 = lattice_in_same_ambient_space(L, identity_matrix(QQ, rank(L))[1:rank(N),:])
      @hassert  :LatWithIsom 1 genus(N) == genus(N2)
      M2 = lattice_in_same_ambient_space(L, identity_matrix(QQ,rank(L))[rank(N)+1:end, :])
      @hassert :LatWithIsom 1 genus(M) == genus(M2)
      push!(results, (L, M2, N2))
      @vprint :LatWithIsom 1 "Gluing done"
      GC.gc()
    end
  end
  return results
end

function _isomorphism_classes_primitive_extensions_along_elementary(N::ZLat, M::ZLat, H::TorQuadModule)
  @hassert :LatWithIsom 1 is_one(basis_matrix(N))
  @hassert :LatWithIsom 1 is_one(basis_matrix(M))
  q = elementary_divisors(H)[end]
  ok, p, _ = is_prime_power_with_data(q)
  @hassert :LatWithIsom 1 ok
  @hassert :LatWithIsom 1 is_elementary(M, p)
  results = Tuple{ZLat, ZLat, ZLat}[]
  GN, _ = image_in_Oq(N)
  GM, _ = image_in_Oq(M)
  qN = domain(GN)
  qM = domain(GM)

  D, inj = direct_sum(qN, qM)
  qNinD, qMinD = inj
  OD = orthogonal_group(D)
  VN, VNinqN, _ = _get_V(N, qN, identity_matrix(QQ, rank(N)), id_hom(qN), minpoly(identity_matrix(QQ,1)), p)
  subsN = _subgroups_orbit_representatives_and_stabilizers_elementary(VNinqN, GN, p)
  filter!(HN -> is_anti_isometric_with_anti_isometry(domain(HN[1]), H), subsN)
  @assert !isempty(subsN)
  subsM = _subgroups_orbit_representatives_and_stabilizers_elementary(id_hom(qM), GM, p)
  filter!(HM -> is_isometric_with_isometry(domain(HM[1]), H), subsM)
  @assert !isempty(subsM)

  for H1 in subsN, H2 in subsM
    ok, phi = is_anti_isometric_with_anti_isometry(domain(H1[1]), domain(H2[1]))
    @hassert :LatWithIsom 1 ok

    HNinqN, stabN = H1
    OHN = orthogonal_group(HN)

    HMinqM, stabM = H2
    OHM = orthogonal_group(HM)

    actN = hom(stabN, OHN, [OHN(restrict_automorphism(x, HNinqN)) for x in gens(stabN)])

    actM = hom(stabM, OHM, [OHM(restrict_automorphism(x, HMinqM)) for x in gens(stabM)])
    imM, _ = image(actM)

    stabNphi = AutomorphismGroupElem{TorQuadModule}[OHM(compose(inv(phi), compose(hom(actN(g)), phi))) for g in gens(stabN)]
    stabNphi, _ = sub(OHM, stabNphi)
    reps = double_cosets(OHM, stabNphi, imM)
    @vprint :LatWithIsom 1 "$(length(reps)) isomorphism classe(s) of primitive extensions"
    for g in reps
      g = representative(g)
      phig = compose(phi, hom(g))
      _glue = Vector{QQFieldElem}[lift(qNinD(HNinqN(g))) + lift(qMinD(HMinqM(phig(g)))) for g in gens(domain(phig))]
      z = zero_matrix(QQ, 0, degree(N)+degree(M))
      glue = reduce(vcat, [matrix(QQ, 1, degree(N)+degree(M), g) for g in _glue], init = z)
      glue = vcat(identity_matrix(QQ, rank(N)+rank(M)), glue)
      glue = FakeFmpqMat(glue)
      _B = hnf(glue)
      _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
      L = lattice(ambient_space(cover(D)), _B[end-rank(N)-rank(M)+1:end, :])
      N2 = lattice_in_same_ambient_space(L, identity_matrix(QQ, rank(L))[1:rank(N),:])
      @hassert :LatWithIsom 1 genus(N) == genus(N2)
      M2 = lattice_in_same_ambient_space(L, identity_matrix(QQ,rank(L))[rank(N)+1:end, :])
      @hassert :LatWithIsom 1 genus(M) == genus(M2)
      push!(results, (L, M2, N2))
      @vprint :LatWithIsom 1 "Gluing done"
      GC.gc()
    end
  end
  return results
end


@doc Markdown.doc"""
    primitive_embeddings_in_primary_lattice(L::ZLat, M::ZLat)
                                     -> Vector{Tuple{ZLat, ZLat, ZLat}}

Given a `p`-primary lattice `L`, unique in its genus, and a lattice `M`,
compute representatives for all isomorphism classes of primitive embeddings
of `M` in `L` up to the actions of $\bar{O}(M)$ and $O(L)$. Here
$\bar{O}(M)$ denotes the image of $O(M)\to O(q_M)$.

The output is given in terms of triples `(L', M', N')` where `L'` is
isometric to `L`, `M'` is a sublattice of `L'` isometric to `M` and
`N'` is the orthogonal complement of `M'` in `L'`.
"""
function primitive_embeddings_in_primary_lattice(L::ZLat, M::ZLat, GL::AutomorphismGroup{TorQuadModule} = orthogonal_group(discriminant_group(L)); check::Bool = false)
  bool, p = is_primary_with_prime(L)
  @req bool "L must be unimodular or primary"
  el = is_elementary(L, p)
  if check
    @req length(genus_representatives(L)) == 1 "L must be unique in its genus"
  end
  M = Zlattice(gram = gram_matrix(M))
  results = Tuple{ZLat, ZLat, ZLat}[]
  qL = rescale(discriminant_group(L), -1)
  GL = Oscar._orthogonal_group(qL, matrix.(gens(GL)))
  pL, _, nL = signature_tuple(L)
  pM, _, nM = signature_tuple(M)
  @req (pL-pM >= 0 && nL-nM >= 0) "Impossible embedding"
  if ((pL, nL) == (pM, nM))
    genus(M) != genus(L) && return results
    return [(M, M, lattice_in_same_ambient_space(M, zero_matrix(QQ, 0, degree(M))))]
  end
  qM = discriminant_group(M)
  D, inj, proj = biproduct(qM, qL)
  qMinD, qLinD = inj
  if el
    VM, VMinqM, _ = _get_V(M, qM, identity_matrix(QQ, rank(M)), id_hom(qM), minpoly(identity_matrix(QQ,1)), p)
  else
    VM, VMinqM = primary_part(qM, p)
  end
  for k  in divisors(gcd(order(VM), order(qL)))
    @vprint :LatWithIsom 1 "Glue order: $(k)"
    if el
      subsL = subgroups_orbit_representatives_and_stabilizers_elementary(id_hom(GL), GL, k)
    else
      subsL = subgroups_orbit_representatives_and_stabilizers(GL, order = k)
    end
    @vprint :LatWithIsom 1 "$(length(subsL)) subgroup(s)"
    for H in subsL
      HL = domain(H[1])
      it = subgroups(abelian_group(VM), order = order(HL))
      subsM = [sub(qM, VMinqM.(VM.(j[2].(gens(j[1])))))[2] for j in it]
      filter!(HM -> is_anti_isometric_with_anti_isometry(domain(HM), HL)[1], subsM)
      isempty(subsM)  && continue
      @vprint :LatWithIsom 1 "Possible gluings"
      HM = subsM[1]
      ok, phi = is_anti_isometric_with_anti_isometry(domain(HM), HL)
      @hassert :LatWithIsom 1 ok
      _glue = [lift(qMinD(HM(g))) + lift(qLinD(H[1](phi(g)))) for g in gens(domain(HM))]
      ext, _ = sub(D, D.(_glue))
      perp, j = orthogonal_submodule(D, ext)
      disc = torsion_quadratic_module(cover(perp), cover(ext), modulus = modulus_bilinear_form(perp),
                                                               modulus_qf = modulus_quadratic_form(perp))
      disc = rescale(disc, -1)
      !is_genus(disc, (pL-pM, nL-nM))  && continue
      G = genus(disc, (pL-pM, nL-nM))
      @vprint :LatWithIsom 1 "We can glue: $G"
      Ns = representatives(G)
      @vprint :LatWithIsom 1 "$(length(Ns)) possible orthogonal complement(s)"
      Ns = lll.(Ns)
      Ns = ZLat[Zlattice(gram=gram_matrix(N)) for N in Ns]
      qM2, _ = sub(qM, [proj[1](j(g)) for g in gens(perp)])
      for N in Ns
        append!(results, _isomorphism_classes_primitive_extensions(N, M, qM2))
        GC.gc()
      end
      GC.gc()
    end
  end
  @hassert :LatWithIsom 1 all(triple -> genus(triple[1]) == genus(L), results)
  return results
end

#function primitive_embeddings_of_elementary_lattice(L::ZLat, M::ZLat, GL::AutomorphismGroup{TorQuadModule} = orthogonal_group(discriminant_group(L)); classification::Bool = false, first::Bool = false, check::Bool = false)
#  bool, p = is_elementary_with_prime(M)
#  @req bool "M must be elementary"
#  if check
#    @req length(genus_representatives(L)) == 1 "L must be unique in its genus"
#  end
#  results = Tuple{ZLat, ZLat, ZLat}[]
#  qL = rescale(discriminant_group(L), -1)
#  GL = Oscar._orthogonal_group(qL, matrix.(gens(GL)))
#  pL, _, nL = signature_tuple(L)
#  pM, _, nM = signature_tuple(M)
#  @req (pL-pM >= 0 && nL-nM >= 0) "Impossible embedding"
#  if ((pL, nL) == (pM, nM))
#    if genus(M) != genus(L) 
#      return false, results
#    else
#      return true, [(M, M, lattice_in_same_ambient_space(M, zero_matrix(QQ, 0, degree(M))))]
#    end
#  end
#  qM = discriminant_group(M)
#  D, inj, proj = biproduct(qM, qL)
#  qMinD, qLinD = inj
#  VL, VLinqL, _ = _get_V(L, qL, identity_matrix(QQ, rank(L)), id_hom(qL), minpoly(identity_matrix(QQ,1)), p)
#  for k  in divisors(gcd(order(qM), order(VL)))
#    @info "Glue order: $(k)"
#    subsL = subgroups_orbit_representatives_and_stabilizers_elementary(VLinqL, GL, k)
#    @info "$(length(subsL)) subgroup(s)"
#    subsM = subgroups_orbit_representatives_and_stabilizers_elementary(id_hom(qM), orthogonal_group(qM), k)
#    for H in subsL
#      HL = domain(H[1])
#      _subsM = filter(HM -> is_anti_isometric_with_anti_isometry(domain(HM[1]), HL)[1], subsM)
#      isempty(_subsM)  && continue
#      @info "Possible gluing"
#      HM = _subsM[1][1]
#      ok, phi = is_anti_isometric_with_anti_isometry(domain(HM), HL)
#      @assert ok
#      _glue = [lift(qMinD(HM(g))) + lift(qLinD(H[1](phi(g)))) for g in gens(domain(HM))]
#      ext, _ = sub(D, D.(_glue))
#      perp, j = orthogonal_submodule(D, ext)
#      disc = torsion_quadratic_module(cover(perp), cover(ext), modulus = modulus_bilinear_form(perp),
#                                                               modulus_qf = modulus_quadratic_form(perp))
#      disc = rescale(disc, -1)
#      !is_genus(disc, (pL-pM, nL-nM))  && continue
#      G = genus(disc, (pL-pM, nL-nM))
#      !classification && return true, G
#      @info "We can glue: $G"
#      Ns = representatives(G)
#      @info "$(length(Ns)) possible orthogonal complement(s)"
#      Ns = lll.(Ns)
#      Ns = ZLat[Zlattice(gram=gram_matrix(N)) for N in Ns]
#      qM2, _ = sub(qM, [proj[1](j(g)) for g in gens(perp)])
#      for N in Ns
#        append!(results, _isomorphism_classes_primitive_extensions_along_elementary(N, M, qM2))
#        if first
#          return results
#        end
#        GC.gc()
#      end
#      GC.gc()
#    end
#  end
#  @assert all(triple -> genus(triple[1]) == genus(L), results)
#  return (length(results) >0), results
#end

####################################################################################
#
# Admissible equivariant primitive extensions
#
####################################################################################

#function equivariant_primitive_extensions(A::LatWithIsom, GA::AutomorphismGroup{TorQuadModule},
#                                         B::LatWithIsom, GB::AutomorphismGroup{TorQuadModule},
#                                         L::ZLat; check::Bool = false)
#  
#  if check
#    pA, nA = signature_tuple(A)
#    pB, nB = signature_tuple(B)
#    pL, nL = signature_tuple(L)
#    @req pA+pB == pL "Incompatible signatures"
#    @req nA+nB == nL "Incompatible signatures"
#    @req ambient_space(A) === ambient_space(B) === ambient_space(L) "Lattices must all live in the same ambient space"
#  end
#
#  results = LatWithIsom[]
#
#  qA, fqA = discriminant_group(A)
#  qB, fqB = discriminant_group(B)
#  
#  if check
#    @req all(g -> compose(g, compose(fqA, inv(g))) == fqA, GA) "GA does not centralize fqA"
#    @req all(g -> compose(g, compose(fqB, inv(g))) == fqB, GB) "GB does not centralize fqB"
#  end
#  D, qAinD, qBinD, OD, OqAinOD, OqBinOD = _sum_with_embeddings_orthogonal_groups(qA, qB)
#  OqA = domain(OqAinOD)
#  OqB = domain(OqBinOD)
#
#  gamma, _, _ = glue_map(L, lattice(A), lattice(B))
#  subsA = classes_conjugate_subgroups(GA, domain(gamma))
#  @assert !isempty(subsN)
#  subsB = classes_conjugate_subgroups(GB, codomain(gamma))
#  @assert !isempty(subsM)
#
#  for (H1, H2) in Hecke.cartesian_product_iterator([subsA, subsB], inplace=true)
#    ok, phi = is_anti_isometric_with_anti_isometry(H1, H2)
#    @assert ok
#    SAinqA, stabA = H1
#    OSAinOqA = embedding_orthogonal_group(SAinqA)
#    OSA = domain(OSAinOqA)
#    OSAinOD = compose(OSAinOqA, OqAinOD)
#
#    SBinqB, stabB = H2
#    SB = domain(SBinqB)
#    OSBinOqB = embedding_orthogonal_group(SBinqB)
#    OSB = domain(OSBinOqB)
#    OSBinOD = compose(OSBinOqB, OqBinOD)
#
#    # we compute the image of the stabalizers in the respective OS* and we keep track
#    # of the elements of the stabilizers acting trivially in the respective S*
#    actA = hom(stabA, OSA, [OSA(Oscar.restrict_automorphism(x, SAinqA)) for x in gens(stabA)])
#    imA, _ = image(actA)
#    kerA = AutomorphismGroupElem{TorQuadModule}[OqAinOD(x) for x in gens(Hecke.kernel(actA)[1])]
#    fSA = OSA(restrict_automorphism(fqA, SAinqA))
#
#    actB = hom(stabB, OSB, [OSB(Oscar.restrict_automorphism(x, SBinqB)) for x in gens(stabB)])
#    imB, _ = image(actB)
#    kerB = AutomorphismGroupElem{TorQuadModule}[OqBinOD(x) for x in gens(Hecke.kernel(actB)[1])]
#    fSB = OSB(restrict_automorphism(fqB, SBinqB))
#
#
#    fSAinOSB = OSB(compose(inv(phi), compose(hom(fSA), phi)))
#    bool, g0 = representative_action(OSB, fSAinOSB, fSB)
#    bool || continue
#    phi = compose(phi, hom(OSB(g0)))
#    fSAinOSB = OSB(compose(inv(phi), compose(hom(fSA), phi)))
#    # Now the new phi is "sending" the restriction of fA to this of fB.
#    # So we can glue SA and SB.
#    @assert fSAinOSB == fSB
#
#    # Now it is time to compute generators for O(SB, rho_l(qB), fB), and the induced
#    # images of stabA|stabB for taking the double cosets next
#    center, _ = centralizer(OSB, fSB)
#    center, _ = sub(OSB, [OSB(c) for c in gens(center)])
#    stabSAphi = AutomorphismGroupElem{TorQuadModule}[OSB(compose(inv(phi), compose(hom(actA(g)), phi))) for g in gens(stabA)]
#    stabSAphi, _ = sub(center, stabSAphi)
#    stabSB, _ = sub(center, [actB(s) for s in gens(stabB)])
#    reps = double_cosets(center, stabSB, stabSAphi)
#
#    for g in reps
#      g = representative(g)
#      phig = compose(phi, hom(g))
#
#      _glue = Vector{QQFieldElem}[lift(g) + lift(phig(g)) for g in gens(domain(phig))]
#      z = zero_matrix(QQ, 0, degree(A))
#      glue = reduce(vcat, [matrix(QQ, 1, degree(A), g) for g in _glue], init=z)
#      glue = vcat(basis_matrix(lattice(A)+lattice(B)), glue)
#      glue = FakeFmpqMat(glue)
#      _B = hnf(glue)
#      _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
#      C2 = lattice(ambient_space(A), _B[end-rank(A)-rank(B)+1:end, :])
#      fC2 = block_diagonal_matrix([isometry(A), isometry(B)])
#      _B = solve_left(reduce(vcat, basis_matrix.([A,B])), basis_matrix(C2))
#      fC2 = _B*fC2*inv(_B)
#      L2 = lattice_with_isometry(C2, fC2, ambient_representation = false)
#      @assert genus(L2) == genus(L)
#      push!(results, L2)
#    end
#  end
#  return results
#end

@doc Markdown.doc"""
    admissible_equivariant_primitive_extensions(Afa::LatWithIsom,
                                                Bfb::LatWithIsom,
                                                Cfc::LatWithIsom,
                                                p::Integer,
                                                q::Integer = p; check=true)
                                                     -> Vector{LatWithIsom}

Given a triple of lattices with isometry `(A, fa)`, `(B, fb)` and `(C, fc)` and a
prime number `p`, such that `(A, B, C)` is `p`-admissible, return a set of
representatives of the double coset $G_B\backslash S\slash/G_A$ where:

  - $G_A$ and $G_B$ are the respective images of the morphisms
    $O(A, fa) -> O(q_A, \bar{fa})$ and $O(B, fb) -> O(q_B, \bar{fb})$;
  - $S$ is the set of all primitive extensions $A \perp B \subseteq C'$ with isometry
    $fc'$ where $p\cdot C' \subseteq A\perpB$ and such that the type of $(C', fc'^q)$
    is equal to the type of `(C, fc)`.

If `check == true` the input triple is checked to a `p`-admissible triple of
integral lattices (with isometry) with `fA` and `fB` having relatively coprime
irreducible minimal polynomials and imposing that `A` and `B` are orthogonal
if `A`, `B` and `C` lie in the same ambient quadratic space.

Note that `Afa` and `Bfb` must be of pure type, i.e. the minimal polynomials
of the associated isometries must be irreducible (and relatively coprime).

See Algorithm 2 of [BH22].
"""
function admissible_equivariant_primitive_extensions(A::LatWithIsom,
                                                     B::LatWithIsom,
                                                     C::LatWithIsom,
                                                     p::Integer,
                                                     q::Integer = p; check=true)
  # requirement for the algorithm of BH22
  @req is_prime(p) "p must be a prime number" 

  if check 
    @req all(L -> is_integral(L), [A, B, C]) "Underlying lattices must be integral"
    chiA = minpoly(A)
    chiB = minpoly(parent(chiA), isometry(B))
    @req gcd(chiA, chiB) == 1 "Minimal irreducible polynomials must be relatively coprime"
    @req is_admissible_triple(A, B, C, p) "Entries, in this order, do not define an admissible triple"
    if ambient_space(A) === ambient_space(B) === ambient_space(C)
      G = gram_matrix(ambient_space(C))
      @req iszero(basis_matrix(A)*G*transpose(basis_matrix(B))) "Lattice in same ambient space must be orthogonal"
    end 
  end 

  results = LatWithIsom[]

  # this is the glue valuation: it is well-defined because the triple in input is admissible
  g = div(valuation(divexact(det(A)*det(B), det(C)), p), 2)

  qA, fqA = discriminant_group(A)
  qB, fqB = discriminant_group(B)
  GA = image_centralizer_in_Oq(A)
  @hassert :LatWithIsom 1 fqA in GA
  GB = image_centralizer_in_Oq(B)
  @hassert :LatWithIsom 1 fqB in GB

  # this is where we will perform the glueing
  if ambient_space(A) === ambient_space(B)
    D, qAinD, qBinD, OD, OqAinOD, OqBinOD = _sum_with_embeddings_orthogonal_groups(qA, qB)
  else
    D, qAinD, qBinD, OD, OqAinOD, OqBinOD = _direct_sum_with_embeddings_orthogonal_groups(qA, qB)
  end 

  OqA = domain(OqAinOD)
  OqB = domain(OqBinOD)

  # if the glue valuation is zero, then we glue along the trivial group and we don't
  # have much more to do. Since the triple is p-admissible, A+B = C
  if g == 0
    geneA = AutomorphismGroupElem{TorQuadModule}[OqAinOD(OqA(a.X)) for a in gens(GA)]
    geneB = AutomorphismGroupElem{TorQuadModule}[OqBinOD(OqB(b.X)) for b in gens(GB)]
    gene = vcat(geneA, geneB)
    GC2, _ = sub(OD, gene)
    if ambient_space(A) === ambient_space(B) === ambient_space(C)
      C2 = lattice(A)+lattice(B)
      fC2 = block_diagonal_matrix([isometry(A), isometry(B)])
      _B = solve_left(reduce(vcat, basis_matrix.([A,B])), basis_matrix(C2))
      fC2 = _B*fC2*inv(_B)
      @hassert :LatWithIsom 1 fC2*gram_matrix(C2)*transpose(fC2) == gram_matrix(C2)
    else
      _B = block_diagonal_matrix([basis_matrix(A), basis_matrix(B)])
      C2 = lattice_in_same_ambient_space(cover(D), _B)
      fC2 = block_diagonal_matrix([isometry(A), isometry(B)])
      @hassert :LatWithIsom 1 fC2*gram_matrix(C2)*transpose(fC2) == gram_matrix(C2)
    end
    qC2 = discriminant_group(C2)
    phi2 = hom(qC2, D, [D(lift(x)) for x in gens(qC2)])
    @hassert :LatWithIsom 1 _is_isometry(phi2)
    if is_of_type(lattice_with_isometry(C2, fC2^q, ambient_representation=false), type(C))
      GC = Oscar._orthogonal_group(qC2, [compose(phi2, compose(hom(g), inv(phi2))).map_ab.map for g in gens(GC2)])
      C2fc2 = lattice_with_isometry(C2, fC2, ambient_representation=false)
      @hassert :LatWithIsom 1 discriminant_group(C2fc2)[2] in GC
      set_attribute!(C2fc2, :image_centralizer_in_Oq, GC)
      push!(results, C2fc2)
    end
    return results
  end
  # these are GA|GB-invariant, fA|fB-stable, and should contain the kernels of any glue map
  VA, VAinqA, fVA = _get_V(hom(fqA), minpoly(B), p)
  VB, VBinqB, fVB = _get_V(hom(fqB), minpoly(A), p)

  # since the glue kernels must have order p^g, in this condition, we have nothing
  if min(order(VA), order(VB)) < p^g
    return results
  end

  # scale of the dual: any glue kernel must contain the multiples of l of the respective
  # discriminant groups
  l = valuation(level(genus(C)), p)
  
  # We look for the GA|GB-invariant and fA|fB-stable subgroups of VA|VB which respectively
  # contained lqA|lqB. This is done by computing orbits and stabilisers of VA/lqA (resp VB/lqB)
  # seen as a F_p-vector space under the action of GA (resp. GB). Then we check which ones
  # are fA-stable (resp. fB-stable)

  subsA = _subgroups_orbit_representatives_and_stabilizers_elementary(VAinqA, GA, p^g, fVA, ZZ(l))
  subsB = _subgroups_orbit_representatives_and_stabilizers_elementary(VBinqB, GB, p^g, fVB, ZZ(l))
  # once we have the potential kernels, we create pairs of anti-isometric groups since glue
  # maps are anti-isometry
  R = Tuple{eltype(subsA), eltype(subsB), TorQuadModuleMor}[]
  for H1 in subsA, H2 in subsB
    ok, phi = is_anti_isometric_with_anti_isometry(domain(H1[1]), domain(H2[1]))
    !ok && continue
    push!(R, (H1, H2, phi))
  end

  # now, for each pair of anti-isometric potential kernels, we need to see whether
  # it is (fA,fB)-equivariant, up to conjugacy. For each working pair, we compute the
  # corresponding overlattice and check whether it satisfies the type condition
  for (H1, H2, phi) in R
    SAinqA, stabA = H1
    SA = domain(SAinqA)
    OSA = orthogonal_group(SA)

    SBinqB, stabB = H2
    SB = domain(SBinqB)
    OSB = orthogonal_group(SB)

    # we compute the image of the stabalizers in the respective OS* and we keep track
    # of the elements of the stabilizers acting trivially in the respective S*
    actA = hom(stabA, OSA, [OSA(restrict_automorphism(x, SAinqA)) for x in gens(stabA)])
    imA, _ = image(actA)
    kerA = AutomorphismGroupElem{TorQuadModule}[OqAinOD(x) for x in gens(kernel(actA)[1])]
    push!(kerA, OqAinOD(one(OqA)))
    fSA = OSA(restrict_automorphism(fqA, SAinqA))

    actB = hom(stabB, OSB, [OSB(restrict_automorphism(x, SBinqB)) for x in gens(stabB)])
    imB, _ = image(actB)
    kerB = AutomorphismGroupElem{TorQuadModule}[OqBinOD(x) for x in gens(Hecke.kernel(ctB)[1])]
    push!(kerB, OqBinOD(one(OqB)))
    fSB = OSB(restrict_automorphism(fqB, SBinqB))

    # we get all the elements of qB of order exactly p^{l+1}, which are not mutiple of an
    # element of order p^{l+2}. In theory, glue maps are classified by the orbit of phi
    # under the action of O(SB, rho_{l+1}(qB), fB)
    rB, rBinqB = _rho_functor(qB, p, l+1)
    @hassert :LatWithIsom 1 is_invariant(stabB, rBinqB)
    rBinSB = hom(rB, SB, TorQuadModuleElem[SBinqB\(rBinqB(k)) for k in gens(rB)])
    @hassert :LatWithIsom 1 is_injective(rBinSB) # we indeed have rho_{l+1}(qB) which is a subgroup of SB

    # We compute the generators of O(SB, rho_{l+1}(qB))
    OSBrB, _ = stabilizer(OSB, rBinSB)
    @hassert :LatWithIsom fSB in OSBrB

    # phi might not "send" the restriction of fA to this of fB, but at least phi*fA*phi^-1
    # should be conjugate to fB inside O(SB, rho_l(qB)) for the glueing.
    # If not, we try the next potential pair.
    fSAinOSBrB = OSBrB(compose(inv(phi), compose(hom(fSA), phi)))
    bool, g0 = representative_action(OSBrB, fSAinOSBrB, OSBrB(fSB))
    bool || continue
    phi = compose(phi, hom(OSB(g0)))
    fSAinOSBrB = OSB(compose(inv(phi), compose(hom(fSA), phi)))
    # Now the new phi is "sending" the restriction of fA to this of fB.
    # So we can glue SA and SB.
    @hassert :LatWithIsom 1 fSAinOSBrB == fSB

    # Now it is time to compute generators for O(SB, rho_l(qB), fB), and the induced
    # images of stabA|stabB for taking the double cosets next
    center, _ = centralizer(OSBrB, OSBrB(fSB))
    center, _ = sub(OSB, [OSB(c) for c in gens(center)])
    stabSAphi = AutomorphismGroupElem{TorQuadModule}[OSB(compose(inv(phi), compose(hom(actA(g)), phi))) for g in gens(stabA)]
    stabSAphi, _ = sub(center, stabSAphi)
    stabSB, _ = sub(center, [actB(s) for s in gens(stabB)])
    reps = double_cosets(center, stabSB, stabSAphi)

    # now we iterate over all double cosets and for each representative, we compute the
    # corresponding overlattice in the glueing. If it has the wanted type, we compute
    # the image of the centralizer in OD from the stabA and stabB.

    for g in reps
      g = representative(g)
      phig = compose(phi, hom(g))

      if ambient_space(A) === ambient_space(B)
        _glue = Vector{QQFieldElem}[lift(g) + lift(phig(g)) for g in gens(domain(phig))]
        z = zero_matrix(QQ, 0, degree(A))
        glue = reduce(vcat, [matrix(QQ, 1, degree(A), g) for g in _glue], init=z)
        glue = vcat(basis_matrix(lattice(A)+lattice(B)), glue)
        glue = FakeFmpqMat(glue)
        _B = hnf(glue)
        _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
        C2 = lattice(ambient_space(A), _B[end-rank(A)-rank(B)+1:end, :])
        fC2 = block_diagonal_matrix([isometry(A), isometry(B)])
        _B = solve_left(reduce(vcat, basis_matrix.([A,B])), basis_matrix(C2))
        fC2 = _B*fC2*inv(_B)
      else
        _glue = Vector{QQFieldElem}[lift(qAinD(SAinqA(g))) + lift(qBinD(SBinqB(phig(g)))) for g in gens(domain(phig))]
        z = zero_matrix(QQ,0,degree(A)+degree(B))
        glue = reduce(vcat, [matrix(QQ,1,degree(A)+degree(B),g) for g in _glue], init=z)
        glue = vcat(block_diagonal_matrix([basis_matrix(A), basis_matrix(B)]), glue)
        glue = FakeFmpqMat(glue)
        _B = hnf(glue)
        _B = QQ(1, denominator(glue))*change_base_ring(QQ, numerator(_B))
        C2 = Hecke.lattice(ambient_space(cover(D)), _B[end-rank(A)-rank(B)+1:end, :])
        fC2 = block_diagonal_matrix([isometry(A), isometry(B)])
        __B = solve_left(block_diagonal_matrix(basis_matrix.([A,B])), basis_matrix(C2))
        fC2 = __B*fC2*inv(__B)
        @hassert :LatWithIsom 1 fC2*gram_matrix(C2)*transpose(fC2) == gram_matrix(C2)
      end

      ext, _ = sub(D, D.(_glue))
      perp, j = orthogonal_submodule(D, ext)
      disc = torsion_quadratic_module(cover(perp), cover(ext), modulus = modulus_bilinear_form(perp),
                                                               modulus_qf = modulus_quadratic_form(perp))

      qC2 = discriminant_group(C2)
      OqC2 = orthogonal_group(qC2)
      phi2 = hom(qC2, disc, [disc(lift(x)) for x in gens(qC2)])
      @hassert :LatWithIsom 1 _is_isometry(phi2)
      
      if !is_of_type(lattice_with_isometry(C2, fC2^q, ambient_representation = false), type(C))
        continue
      end

      C2 = lattice_with_isometry(C2, fC2, ambient_representation=false)
      geneOSA =  AutomorphismGroupElem{TorQuadModule}[OSA(compose(phig, compose(hom(g1), inv(phig)))) for g1 in unique(gens(imB))]
      im2_phi, _ = sub(OSA, geneOSA)
      gene_inter = AutomorphismGroupElem{TorQuadModule}[h for h in unique(gens(intersect(imA, im2_phi)[1]))]
      im3, _ = sub(imA, gene_inter)
      stab = Tuple{AutomorphismGroupElem{TorQuadModule}, AutomorphismGroupElem{TorQuadModule}}[(actA\x, actB\(imB(compose(inv(phig), compose(hom(x), phig))))) for x in gens(im3)]
      stab = AutomorphismGroupElem{TorQuadModule}[OqAinOD(x[1])*OqBinOD(x[2]) for x in stab]
      stab = union(stab, kerA)
      stab = union(stab, kerB)
      stab = TorQuadModuleMor[restrict_automorphism(g, j) for g in stab]
      stab = TorQuadModuleMor[hom(disc, disc, [disc(lift(g(perp(lift(l))))) for l in gens(disc)]) for g in stab]
      stab = Oscar._orthogonal_group(qC2, [compose(phi2, compose(g, inv(phi2))).map_ab.map for g in stab])
      @hassert :LatWithIsom 1 discriminant_group(C2)[2] in stab
      set_attribute!(C2, :image_centralizer_in_Oq, stab)
      push!(results, C2)
    end
  end

  return results
end

function _is_isometry(f::TorQuadModuleMor)
  !is_bijective(f) && return false
  for a in gens(domain(f))
    for b in gens(domain(f))
      if f(a)*f(b) != a*b
        return false
      end
    end
    if Hecke.quadratic_product(a) != Hecke.quadratic_product(f(a))
      return false
    end
  end
  return true
end

