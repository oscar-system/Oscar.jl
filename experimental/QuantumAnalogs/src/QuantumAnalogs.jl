################################################################################
# Quantum analogs
#
# Copyright (C) 2020 Ulrich Thiel, ulthiel.com/math
#
# Mar 2023: Imported into OSCAR by UT
#
# Future features could include:
# - Symmetric quantum numbers
# - Two-colored quantum numbers
################################################################################

export quantum_integer, quantum_factorial, quantum_binomial

################################################################################
# Quantum integers
################################################################################
@doc raw"""
    quantum_integer(n::IntegerUnion, q::RingElem)
    quantum_integer(n::IntegerUnion, q::Integer)
    quantum_integer(n::IntegerUnion)

Let ``n âˆˆ â„¤`` and let ``â„š(ğª)`` be the fraction field of the polynomial ring ``â„¤[ğª]`` in
one variable ``ğª``. The **quantum integer** ``[n]_ğª âˆˆ â„š(ğª)`` of ``n`` is defined as
```math
[n]_ğª â‰” \frac{ğª^n-1}{ğª-1} \;.
```
We have
```math
[n]_ğª = \sum_{i=0}^{n-1} ğª^i âˆˆ â„¤[ğª] \quad \text{if } n â‰¥ 0
```
and
```math
[n]_ğª = -ğª^{n} [-n]_ğª \quad \text{for any } n âˆˆ â„¤ \;,
```
hence
```math
[n]_ğª = - \sum_{i=0}^{-n-1} ğª^{n+i} âˆˆ â„¤[ğª^{-1}] \quad \text{ if } n < 0 \;.
```
This shows in particular that actually
```math
[n]_ğª âˆˆ â„¤[ğª,ğª^{-1}] âŠ‚ â„š(ğª) \quad \text{ for any } n âˆˆ â„¤ \;.
```
Now, for an element ``q`` of a ring ``R`` we define ``[n]_q âˆˆ R`` as the specialization of
``[n]_ğª`` in ``q`` using the two equations aboveâ€”assuming that ``q`` is invertible in ``R``
if ``n<0``. Note that for ``q=1`` we obtain
```math
[n]_1 = n \quad \text{for any } n âˆˆ â„¤ \;,
```
so the quantum integers are "deformations" of the usual integers.

# Functions
* `quantum_integer(n::IntegerUnion,q::RingElem)` returns ``[n]_q`` as an element of ``R``,
  where ``R`` is the parent ring of ``q``.
* `quantum_integer(n::IntegerUnion,q::Integer)` returns ``[n]_q``. Here, if ``n >= 0`` or
  ``q = Â± 1``, then ``q`` is considered as an element of ``â„¤``, otherwise it is taken as an
  element of ``â„š``.
* `quantum_integer(n::IntegerUnion)` returns ``[n]_ğª`` as an element of ``â„¤[ğª^{-1}]``.

# Examples
```jldoctest
julia> quantum_integer(3)
q^2 + q + 1

julia> quantum_integer(-3)
-q^-1 - q^-2 - q^-3

julia> quantum_integer(3,2)
7

julia> quantum_integer(-3,2)
-7//8

julia> K,i = cyclotomic_field(4, "i");

julia> quantum_integer(3, i)
i
```

# References
1. [Con00](@cite)
2. [KC02](@cite)
"""
function quantum_integer(n::IntegerUnion, q::RingElem)
  R = parent(q)
  isone(q) && return R(n)

  z = zero(R)
  if n >= 0
    for i = 0:n-1
      z += q^i
    end
  else
    for i = 0:-n-1
      z -= q^(n+i)
    end
  end
  return z
end

function quantum_integer(n::IntegerUnion, q::Integer)
  if n >= 0 || q == 1 || q == -1
    return quantum_integer(n,ZZ(q))
  else
    return quantum_integer(n,QQ(q))
  end
end

function quantum_integer(n::IntegerUnion)
  R,q = laurent_polynomial_ring(ZZ, "q")
  return quantum_integer(n,q)
end


################################################################################
# Quantum factorials
################################################################################
@doc raw"""
    quantum_factorial(n::IntegerUnion, q::RingElem)
    quantum_factorial(n::IntegerUnion, q::Integer)
    quantum_factorial(n::IntegerUnion)

For a non-negative integer ``n`` and an element ``q`` of a ring ``R`` the **quantum
factorial** ``[n]_q! âˆˆ R`` is defined as
```math
[n]_q! â‰” [1]_q â‹… â€¦ â‹… [n]_q âˆˆ R \;.
```
Note that for ``q=1`` we obtain
```math
[n]_1! = n! \quad \text{ for all } n âˆˆ â„¤ \;,
```
hence the quantum factorial is a "deformation" of the usual factorial.

# Functions

The functions `quantum_factorial` work analogously to [`quantum_integer`](@ref).

# Examples
```jldoctest
julia> quantum_factorial(3)
q^3 + 2*q^2 + 2*q + 1

julia> quantum_factorial(3,2)
21

julia> K,i = cyclotomic_field(4, "i");

julia> quantum_factorial(3, i)
i - 1
```
"""
function quantum_factorial(n::IntegerUnion, q::RingElem)
  @req n >= 0 "n >= 0 required"

  R = parent(q)
  isone(q) && return R(factorial(n))

  z = one(R)
  for i = 1:n
    z *= quantum_integer(i,q)
  end
  return z
end

function quantum_factorial(n::IntegerUnion, q::Integer)
  return quantum_factorial(n,ZZ(q))
end

function quantum_factorial(n::IntegerUnion)
  R,q = laurent_polynomial_ring(ZZ, "q")
  return quantum_factorial(n,q)
end


################################################################################
# Quantum binomials
################################################################################

@doc raw"""
    quantum_binomial(n::IntegerUnion, q::RingElem)
    quantum_binomial(n::IntegerUnion, k::IntegerUnion, q::Integer)
    quantum_binomial(n::IntegerUnion, k::IntegerUnion)

Let ``k`` be a non-negative integer and let ``n âˆˆ â„¤``. The **quantum binomial**
``\begin{bmatrix} n \\ k \end{bmatrix}_ğª \in â„š(ğª)`` is defined as
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª â‰” \frac{[n]_ğª!}{[k]_ğª! [n-k]_ğª!} = \frac{[n]_ğª [n-1]_ğªâ‹… â€¦ â‹… [n-k+1]_ğª}{[k]_ğª!}
```
Note that the first expression is only defined for ``n â‰¥ k`` since the quantum factorials
are only defined for non-negative integersâ€”but the second  expression is well-defined for
all ``n âˆˆ â„¤`` and is used for the definition. In [Con00](@cite) it is shown that
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª = \sum_{i=0}^{n-k} q^i \begin{bmatrix} i+k-1 \\ k-1 \end{bmatrix}_ğª \quad \text{if } n â‰¥ k > 0 \;.
```
Since
```math
\begin{bmatrix} n \\ 0 \end{bmatrix}_ğª = 1 \quad \text{for all } n âˆˆ â„¤
```
and
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª = 0 \quad \text{if } 0 â‰¤ n < k \;,
```
it follows inductively that
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª âˆˆ â„¤[ğª] \quad \text{if } n â‰¥ 0 \;.
```
For all ``n âˆˆ â„¤`` we have the relation
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª = (-1)^k ğª^{-k(k-1)/2+kn} \begin{bmatrix} k-n-1 \\ k \end{bmatrix}_ğª \;,
```
which shows that
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª âˆˆ â„¤[ğª^{-1}] \quad \text{if } n < 0 \;.
```
In particular,
```math
\begin{bmatrix} n \\ k \end{bmatrix}_ğª âˆˆ â„¤[ğª,ğª^{-1}] \quad \text{for all } n âˆˆ â„¤ \;.
```
Now, for an element ``q`` of a ring ``R`` we define ``\begin{bmatrix} n \\ k
\end{bmatrix}_q`` as the specialization of ``\begin{bmatrix} n \\ k
\end{bmatrix}_{\mathbf{q}}`` in ``q``, where ``q`` is assumed to be invertible in ``R`` if
``n < 0``.

Note that for ``q=1`` we obtain
```math
\begin{bmatrix} n \\ k \end{bmatrix}_1 = {n \choose k} \;,
```
hence the quantum binomial coefficient is a "deformation" of the usual binomial coefficient.

# Functions

The functions `quantum_binomial` work analogously to [`quantum_integer`](@ref).

# Examples
```jldoctest
julia> quantum_binomial(4,2)
q^4 + q^3 + 2*q^2 + q + 1

julia> quantum_binomial(19,5,-1)
36

julia> K,i = cyclotomic_field(4);

julia> quantum_binomial(17,10,i)
0
```

# References
1. [Con00](@cite)
"""
function quantum_binomial(n::IntegerUnion, k::IntegerUnion, q::RingElem)
  @req k >= 0 "k >= 0 required"

  R = parent(q)
  isone(q) && return R(binomial(n,k))
  k == 0 && return one(R)
  k == 1 && return quantum_integer(n,q)
  n < 0 && return (-1)^k * q^(div(-k*(k-1),2) + k*n) * quantum_binomial(k-n-1,k,q)
  n < k && return zero(R)

  z = zero(R)
  for i = 0:n-k
    z += q^i * quantum_binomial(i+k-1,k-1,q)
  end
  return z
end

function quantum_binomial(n::IntegerUnion, k::IntegerUnion, q::Integer)
  if n > 0
    return quantum_binomial(n,k,ZZ(q))
  else
    return quantum_binomial(n,k,QQ(q))
  end
end

function quantum_binomial(n::IntegerUnion, k::IntegerUnion)
  R,q = laurent_polynomial_ring(ZZ, "q")
  return quantum_binomial(n,k,q)
end
