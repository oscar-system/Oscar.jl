#######################################
# Method for symplectic doubling of a matrix group over a field
#######################################

@doc raw"""
    symplectic_doubling(G::MatGroup{T}) -> MatGroup{T} where {T <: FieldElem}

Calculate the symplectic doubling of `G`, that is the matrix group generated by the elements
$\begin{pmatrix} g & 0 \\ 0 & (g^{-1})^T \end{pmatrix}$ for $g \in G$.

# Examples
```jldoctest
julia> S3 = symmetric_group(3)
Symmetric group of degree 3

julia> mat_gens = [permutation_matrix(QQ, g) for g in gens(S3)]
2-element Vector{QQMatrix}:
 [0 1 0; 0 0 1; 1 0 0]
 [0 1 0; 1 0 0; 0 0 1]

julia> G = matrix_group(mat_gens)
Matrix group of degree 3
  over rational field

julia> H = symplectic_doubling(G)
Matrix group of degree 6
  over rational field
  
julia> gens(H)
2-element Vector{MatGroupElem{QQFieldElem, QQMatrix}}:
 [0 1 0 0 0 0; 0 0 1 0 0 0; 1 0 0 0 0 0; 0 0 0 0 1 0; 0 0 0 0 0 1; 0 0 0 1 0 0]
 [0 1 0 0 0 0; 1 0 0 0 0 0; 0 0 1 0 0 0; 0 0 0 0 1 0; 0 0 0 1 0 0; 0 0 0 0 0 1]
```
"""
function symplectic_doubling(G::MatGroup{T}) where {T <: FieldElem}
  symplectic_generators = MatElem{T}[]

  for g in gens(G)
    A = matrix(g)
    h = block_diagonal_matrix([A, transpose(inv(A))])
    push!(symplectic_generators, h)
  end

  H = matrix_group(symplectic_generators)

  return H
end
