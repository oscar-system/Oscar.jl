julia> gset(alternating_group(4))
G-set of
  alternating group of degree 4
  with seeds 1:4

julia> G = dihedral_group(6);

julia> U = sub(G, [g for g in gens(G) if order(g) == 2])[1]
Sub-pc group of order 2

julia> r = right_cosets(G, U)
Right cosets of
  sub-pc group of order 2 in
  pc group of order 6

julia> acting_group(r)
Pc group of order 6

julia> collect(r)
3-element Vector{GroupCoset{PcGroup, SubPcGroup, PcGroupElem}}:
 Right coset of U with representative <identity> of ...
 Right coset of U with representative f2
 Right coset of U with representative f2^2

julia> action_function(r)
* (generic function with 1736 methods)

julia> permutation(r, G[1])
(2,3)

julia> phi = right_coset_action(G,U)
Group homomorphism
  from pc group of order 6
  to permutation group of degree 3 and order 6

julia> phi(G[1]), phi(G[2])
((2,3), (1,2,3))

julia> function optimal_transitive_perm_rep(G)
         is_natural_symmetric_group(G) && return hom(G,G,gens(G))
         is_natural_alternating_group(G) && return hom(G,G,gens(G))
         cand = []  # pairs (U,h) with U â‰¤ G and h a map G -> Sym(G/U)
         for C in subgroup_classes(G)
           U = representative(C)
           h = right_coset_action(G, U)
           is_injective(h) && push!(cand, (U, h))
         end
         return argmax(a -> order(a[1]), cand)[2]
       end;

julia> U = dihedral_group(8)
Pc group of order 8

julia> optimal_transitive_perm_rep(U)
Group homomorphism
  from pc group of order 8
  to permutation group of degree 4 and order 8

julia> isomorphism(PermGroup, U)
Group homomorphism
  from pc group of order 8
  to permutation group of degree 4 and order 8

julia> permutation_group(U)
Permutation group of degree 4 and order 8

julia> for g in all_transitive_groups(degree => 3:8, !is_primitive)
         h = image(optimal_transitive_perm_rep(g))[1]
         if degree(h) < degree(g)
           id = transitive_group_identification(g)
           id_new = transitive_group_identification(h)
           println(id => id_new)
         end
       end
(6, 2) => (3, 2)
(6, 4) => (4, 4)
(6, 7) => (4, 5)
(6, 8) => (4, 5)
(8, 4) => (4, 3)
(8, 13) => (6, 6)
(8, 14) => (4, 5)
(8, 24) => (6, 11)
