```@meta
CurrentModule = Oscar
DocTestSetup = quote
  using Oscar
end
```

```@setup oscar
using Oscar
```

```@contents
Pages = ["subquotients.md"]
```

# Subquotient Modules

A subquotient module is a submodule of a quotient of a free module. In what follows, we simply
use the expression *subquotient* to refer to a subquotient module over a multivariate polynomial ring.
More concretely, given a multivariate polynomial ring $R$, a *subquotient* $M$ over $R$ is a module of type

$M = (\text{im } a + \text{im } b)/\text{im } b,$

where

$a:R^s ⟶R^p \;\text{ and }\; b:R^t ⟶R^p$

are two homomorphisms of free $R$-modules with the same codomain. We then refer to
- the module $M$ as the *subquotient defined by $a$ and $b$*,
- the codomain $R^p$ as the *ambient free module* of $M$,
- the images of the canonical basis vectors of $R^s$ in $R^p$ as the *ambient representatives of the generators* of $M$, and
- the images of the canonical basis vectors of $R^t$ in $R^p$ as the *relations* of $M$.

Alternatively, we speak of the *subquotient of* $\;\text{im } a\;$ *by* $\;\text{im } b\;$ or the
*subquotient defined by $A$ and $B$*, where $A$ and $B$ are the matrices representing
$a$ and $b$, respectively.

Finally, we refer to
- the quotient of $R^p$ by the submodule generated by the relations of $M$ as the *ambient module of $M$*,

and regard $M$ as a submodule of that ambient module, embedded in the natural way.

!!! note
    Recall from the section on free modules that by a free $R$-module we mean a free
    module of type $R^p$ , where we think of $R^p$ as a free module with a given
	basis, namely the basis of standard unit vectors. Accordingly, elements of free modules
	are represented by coordinate vectors, and homomorphisms between free modules by
	matrices. Here, by convention, vectors are row vectors, and matrices operate by
	multiplication on the right.

## Types

All OSCAR types for the finitely presented modules considered here belong to the
abstract type `ModuleFP{T}`, where `T` is the element type of the underlying ring.
The subquotients belong to the abstract subtype `AbstractSubQuo{T} <: ModuleFP{T}`,
they are modelled as objects of the concrete type `SubQuo{T} <: AbstractSubQuo{T}`.

!!! note
    Canonical maps such us the canonical projection onto a quotient module arise in many 
    constructions in commutative algebra. The `SubQuo` type is designed so that it allows
	for the caching of such maps when executing functions. The `tensor_product`
	function discussed in this section provides an example.

## Constructors

```@docs
subquotient(a::FreeModuleHom, b::FreeModuleHom)
```

## Data Associated to Subqotients

If `M` is a subquotient with ambient free `R`-module `F`, then

- `base_ring(M)` refers to `R`,
- `ambient_free_module(M)` to `F`,
- `gens(M)` to the generators of `M`, 
- `ngens(M)` to the number of these generators, 
- `M[i]`, `gen(M, i)` to the `i`th such generator,
- `ambient_representatives_generators(M)` to the ambient representatives of the generators of `M` in `F`,
- `relations(M)` to the relations of `M`, and
- `ambient_module(M)` to the ambient module of `M`.

##### Examples

```jldoctest
julia> R, (x, y, z) = PolynomialRing(QQ, ["x", "y", "z"])
(Multivariate Polynomial Ring in x, y, z over Rational Field, fmpq_mpoly[x, y, z])

julia> F = free_module(R, 1)
Free module of rank 1 over Multivariate Polynomial Ring in x, y, z over Rational Field

julia> A = R[x; y]
[x]
[y]

julia> B = R[x^2; y^3; z^4]
[x^2]
[y^3]
[z^4]

julia> M = SubQuo(F, A, B)
Subquotient of Submodule with 2 generators
1 -> x*e[1]
2 -> y*e[1]
by Submodule with 3 generators
1 -> x^2*e[1]
2 -> y^3*e[1]
3 -> z^4*e[1]

julia> base_ring(M)
Multivariate Polynomial Ring in x, y, z over Rational Field

julia> F === ambient_free_module(M)
true

julia> gens(M)
2-element Vector{SubQuoElem{fmpq_mpoly}}:
 x*e[1]
 y*e[1]

julia> ngens(M)
2

julia> gen(M, 2)
y*e[1]

julia> ambient_representatives_generators(M)
2-element Vector{FreeModElem{fmpq_mpoly}}:
 x*e[1]
 y*e[1]

julia> relations(M)
3-element Vector{FreeModElem{fmpq_mpoly}}:
 x^2*e[1]
 y^3*e[1]
 z^4*e[1]

julia> ambient_module(M)
Subquotient of Submodule with 1 generator
1 -> e[1]
by Submodule with 3 generators
1 -> x^2*e[1]
2 -> y^3*e[1]
3 -> z^4*e[1]

```

## Elements of Subqotients

All OSCAR types for elements of finitely presented modules over multivariate polynomial rings belong to the
abstract type `ModuleElemFP{T}`, where `T` is the element type of the polynomial ring.
For elements of subquotients, there  are the abstract subtype `AbstractSubQuoElem{T} <: ModuleFPElem{T}`
and its concrete descendant `SubQuoElem{T}` which implements an element $m$ of a subquotient
$M$ over a ring $R$ as a sparse row, that is, as an object of type `SRow{T}`.
This object specifies the coefficients of an $R$-linear combination of the generators of $M$
giving $m$. To create an element, enter the coefficients as a sparse row or a vector: 

```@julia
(M::SubQuo{T})(c::SRow{T}) where T
```

```@julia
(M::SubQuo{T})(c::Vector{T}) where T
```

Alternatively, directly write the element as an $R$-linear combination of generators of $M$.

##### Examples

```jldoctest
julia> R, (x, y, z) = PolynomialRing(QQ, ["x", "y", "z"])
(Multivariate Polynomial Ring in x, y, z over Rational Field, fmpq_mpoly[x, y, z])

julia> F = free_module(R, 1)
Free module of rank 1 over Multivariate Polynomial Ring in x, y, z over Rational Field

julia> A = R[x; y]
[x]
[y]

julia> B = R[x^2; y^3; z^4]
[x^2]
[y^3]
[z^4]

julia> M = SubQuo(F, A, B)
Subquotient of Submodule with 2 generators
1 -> x*e[1]
2 -> y*e[1]
by Submodule with 3 generators
1 -> x^2*e[1]
2 -> y^3*e[1]
3 -> z^4*e[1]

julia> m = M(sparse_row(R, [(1,z),(2,one(R))]))
(x*z + y)*e[1]

julia> n = M([z, one(R)])
(x*z + y)*e[1]

julia> o = z*M[1] + M[2]
(x*z + y)*e[1]

julia> m == n == o
true

```

Given an element `m`  of a subquotient `M`  over a multivariate polynomial ring $R$ with element type `T`,
- `parent(m)` refers to `M`, 
- `coordinates(m)` to  an object of type `SRow{T}` specifying the coefficients of an $R$-linear combination of the generators of $M$ which gives $m$, and
- `ambient_representative(m)` to an element of the ambient free module of `M` which represents `m`.

Given an element `f` of the ambient free module of a subquotient `M` such that `f` represents an element of `M`,
the function below creates the represented element:

```@julia
(M::SubQuo{T})(f::FreeModElem{T}; check::Bool = true) where T
```

By default (`check = true`), it is tested whether `f` indeed represents an element of `M`.
If this is already clear, it may be convenient to omit the test (`check = false`).

##### Examples

```jldoctest
julia> R, (x, y, z) = PolynomialRing(QQ, ["x", "y", "z"])
(Multivariate Polynomial Ring in x, y, z over Rational Field, fmpq_mpoly[x, y, z])

julia> F = free_module(R, 1)
Free module of rank 1 over Multivariate Polynomial Ring in x, y, z over Rational Field

julia> A = R[x; y]
[x]
[y]

julia> B = R[x^2; y^3; z^4]
[x^2]
[y^3]
[z^4]

julia> M = SubQuo(F, A, B)
Subquotient of Submodule with 2 generators
1 -> x*e[1]
2 -> y*e[1]
by Submodule with 3 generators
1 -> x^2*e[1]
2 -> y^3*e[1]
3 -> z^4*e[1]

julia> m = z*M[1] + M[2]
(x*z + y)*e[1]

julia> parent(m)
Subquotient of Submodule with 2 generators
1 -> x*e[1]
2 -> y*e[1]
by Submodule with 3 generators
1 -> x^2*e[1]
2 -> y^3*e[1]
3 -> z^4*e[1]

julia> coordinates(m)
Sparse row with positions [1, 2] and values fmpq_mpoly[z, 1]

julia> fm = ambient_representative(m)
(x*z + y)*e[1]

julia> typeof(m)
SubQuoElem{fmpq_mpoly}

julia> typeof(fm)
FreeModElem{fmpq_mpoly}

julia> parent(fm) === ambient_free_module(M)
true

julia> F = ambient_free_module(M)
Free module of rank 1 over Multivariate Polynomial Ring in x, y, z over Rational Field

julia> f = x*F[1]
x*e[1]

julia> M(f)
x*e[1]

julia> typeof(f)
FreeModElem{fmpq_mpoly}

julia> typeof(M(f))
SubQuoElem{fmpq_mpoly}

```

The zero element of a subquotient is obtained as follows:

```@docs
zero(M::SubQuo)
```

Whether a given element of a subquotient is zero can be tested as follows:

```@docs
iszero(m::SubQuoElem)
```

## Tests on Subqotients

```@docs
==(M::SubQuo{T}, N::SubQuo{T}) where T
```

```@docs
issubset(M::SubQuo{T}, N::SubQuo{T}) where T
```

```@docs
iszero(M::SubQuo)
```

## Basic Operations on Subquotients


```@docs
:+(M::SubQuo{T},N::SubQuo{T}) where T
```

```@docs
sum(M::SubQuo{T},N::SubQuo{T}) where T
```

```@docs
intersect(M::SubQuo{T}, N::SubQuo{T}) where T
```

## Submodules and Quotients

```@docs
sub(M::ModuleFP{T}, V::Vector{<:ModuleFPElem{T}}, task::Symbol = :with_morphism) where T
```

```@docs
quo(M::ModuleFP{T}, V::Vector{<:ModuleFPElem{T}}, task::Symbol = :with_morphism) where T
```

## Homomorphisms From Subqotients

All OSCAR types for homomorphisms of finitely presented modules over multivariate polynomial rings belong
to the abstract type `ModuleFPHom{T1, T2}`, where `T1` and `T2` are the types of domain and codomain respectively.
For homomorphisms from subquotients, OSCAR provides the concrete type `SubQuoHom{T1, T2} <: ModuleFPHom{T1, T2}`
as well as the following constructors:

```@docs
hom(M::SubQuo{T}, N::ModuleFP{T}, V::Vector{<:ModuleFPElem{T}}) where T
```

Given a homomorphism of type `SubQuoHom`, a matrix `A` representing it
is recovered by the following function:

```@docs
matrix(a::SubQuoHom)
```

The domain and codomain of a homomorphism `a`  of type `SubQuoHom` can be
recovered by entering `domain(a)` and `codomain(a)`, respectively.
