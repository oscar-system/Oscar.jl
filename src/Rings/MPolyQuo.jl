import Base: ==
export singular_ring, MPolyQuo, MPolyQuoElem, MPolyQuoIdeal
export quo, base_ring, modulus, gens, ngens, dim, simplify!
export issubset
##############################################################################
#
# quotient rings
#
##############################################################################

mutable struct MPolyQuo{S} <: AbstractAlgebra.Ring
  R::MPolyRing
  I::MPolyIdeal{S}
  SQR::Singular.PolyRing  # expensive qring R/I, set and retrived by singular_ring()
  AbstractAlgebra.@declare_other

  function MPolyQuo(R, I) where S
    @assert base_ring(I) == R
    r = new{elem_type(R)}()
    r.R = R
    r.I = I
    return r
  end
end

function show(io::IO, Q::MPolyQuo)
  Hecke.@show_name(io, Q)
  Hecke.@show_special(io, Q)
  io = IOContext(io, :compact => true)
  print(io, "Quotient of $(Q.R) by $(Q.I)")
end

gens(Q::MPolyQuo) = [Q(x) for x = gens(Q.R)]
ngens(Q::MPolyQuo) = ngens(Q.R)
gen(Q::MPolyQuo, i::Int) = Q(gen(Q.R, i))
Base.getindex(Q::MPolyQuo, i::Int) = Q(Q.R[i])
base_ring(W::MPolyQuo) = W.R
modulus(W::MPolyQuo) = W.I

##############################################################################
#
# Quotient ring elements
#
##############################################################################

#TODO: think: do we want/ need to keep f on the Singular side to avoid conversions?
#      or use Bill's divrem to speed things up?
mutable struct MPolyQuoElem{S} <: RingElem
  f::S
  P::MPolyQuo{S}
end

function Base.deepcopy_internal(a::MPolyQuoElem, dict::IdDict)
  return MPolyQuoElem(Base.deepcopy_internal(a.f, dict), a.P)
end

AbstractAlgebra.expressify(a::MPolyQuoElem; context = nothing) = expressify(a.f, context = context)

function show(io::IO, a::MPolyQuoElem)
  print(io, AbstractAlgebra.obj_to_string(a, context = io))
end

##############################################################################
#
# Quotient ring ideals
#
##############################################################################

# For ideals over quotient rings, we would like to delay the expensive
# construction of the singular quotient ring until the user does an operation
# that actually requires it.
mutable struct MPolyQuoIdeal{T} <: Ideal{T}
  I::MPolyIdeal{T}    # ideal in the non-qring, possibly #undef
  SI::Singular.sideal # ideal in the qring, possibly #undef if I isdefined
  base_ring::MPolyQuo
  dim::Int

  function MPolyQuoIdeal(Ox::MPolyQuo{T}, si::Singular.sideal) where T <: MPolyElem
    singular_ring(Ox) == base_ring(si) || error("base rings must match")
    r = new{T}()
    r.base_ring = Ox
    r.SI = si
    r.dim = -1
    return r
  end

  function MPolyQuoIdeal(Ox::MPolyQuo{T}, i::MPolyIdeal{T}) where T <: MPolyElem
    Ox.R == base_ring(i) || error("base rings must match")
    r = new{T}()
    r.base_ring = Ox
    r.I = i
    r.dim = -1
    return r
  end
end

@doc Markdown.doc"""
    base_ring(a::MPolyQuoIdeal)
    
Return the ambient ring of `a`. 

# Examples
julia> R, (x, y, z) = PolynomialRing(QQ, ["x", "y", "z"]);

julia> Q, _ = quo(R, ideal(R, [y-x^2, z-x^3]));

julia> a = ideal(Q, [x, y])
MPolyQuoIdeal(x, y)

julia> base_ring(a)
Quotient of Multivariate Polynomial Ring in x, y, z over Rational Field by ideal generated by: -x^2 + y, -x^3 + z
```
"""
function base_ring(a::MPolyQuoIdeal)
  return a.base_ring
end

function oscar_assure(a::MPolyQuoIdeal)
  isdefined(a, :I) && return
  r = base_ring(a).R
  a.I = ideal(r, r.(gens(a.SI)))
end

function singular_assure(a::MPolyQuoIdeal)
  isdefined(a, :SI) && return
  sa = singular_ring(base_ring(a))
  a.SI = Singular.Ideal(sa, sa.(gens(a.I)))
end

@doc Markdown.doc"""
    gens(a::MPolyQuoIdeal)
    
Return the generators of `a`. 

# Examples
```
"""
function gens(a::MPolyQuoIdeal)
  oscar_assure(a)
  return map(base_ring(a), gens(a.I))
end

# powers, addition and multiplication do not require the singular quotient ring

@doc Markdown.doc"""
    :^(a::MPolyQuoIdeal, m::Int)

Return the m-th power of `a`.  
"""
function Base.:^(a::MPolyQuoIdeal, m::Int)
  if !isdefined(a, :SI)
    return MPolyQuoIdeal(base_ring(a), a.I^m)
  end
  singular_assure(a)
  return MPolyQuoIdeal(base_ring(a), a.SI^m)
end

@doc Markdown.doc"""
    :+(a::MPolyQuoIdeal, b::MPolyQuoIdeal)

Return the sum of `a` and `b`.  
"""
function Base.:+(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
  base_ring(a) == base_ring(b) || error("base rings must match")
  if !isdefined(a, :SI) && !isdefined(b, :SI)
    return MPolyQuoIdeal(base_ring(a), a.I + b.I)
  end
  singular_assure(a)
  singular_assure(b)
  return MPolyQuoIdeal(base_ring(a), a.SI + b.SI)
end

@doc Markdown.doc"""
    :*(a::MPolyQuoIdeal, b::MPolyQuoIdeal)

Return the product of `a` and `b`.  
"""
function Base.:*(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
  base_ring(a) == base_ring(b) || error("base rings must match")
  if !isdefined(a, :SI) && !isdefined(b, :SI)
    return MPolyQuoIdeal(base_ring(a), a.I*b.I)
  end
  singular_assure(a)
  singular_assure(b)
  return MPolyQuoIdeal(base_ring(a), a.SI*b.SI)
end

@doc Markdown.doc"""
    intersect(a::MPolyQuoIdeal, bs::MPolyQuoIdeal...)

Return the intersection of two or more ideals.

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"])
(Multivariate Polynomial Ring in x, y over Rational Field, fmpq_mpoly[x, y])

julia> A, _ = quo(R, ideal(R, [x^2-y^3, x-y]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y defined by a julia-function with inverse
)

julia> a = ideal(A, [y^2])
MPolyQuoIdeal(y^2)

julia> b = ideal(A, [x])
MPolyQuoIdeal(x)

julia> intersect(a,b)
MPolyQuoIdeal(x*y)
```
"""
function intersect(a::MPolyQuoIdeal, bs::MPolyQuoIdeal...)
  singular_assure(a)
  si = a.SI
  for b in bs
    base_ring(b) == base_ring(a) || error("base rings must match")
    singular_assure(b)
    si = Singular.intersection(si, b.SI)
  end
  return MPolyQuoIdeal(base_ring(a), si)
end

#######################################################

@doc Markdown.doc"""
    quotient(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
    
Return the ideal quotient of `a` by `b`. Alternatively, use `a:b`. 

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"]);

julia> Q, _ = quo(R, ideal(R, [x^2-y^3, x-y]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y defined by a julia-function with inverse
)

julia> a = ideal(Q, [y^2])
MPolyQuoIdeal(y^2)

julia> b = ideal(Q, [x])
MPolyQuoIdeal(x)

julia> a:b
MPolyQuoIdeal(y)
```
"""
function quotient(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
  base_ring(a) == base_ring(b) || error("base rings must match")
  singular_assure(a)
  singular_assure(b)
  return MPolyQuoIdeal(base_ring(a), Singular.quotient(a.SI, b.SI))
end
(::Colon)(a::MPolyQuoIdeal, b::MPolyQuoIdeal) = quotient(a, b)

@doc Markdown.doc"""
    iszero(a::MPolyQuoIdeal)

Return true if `a` is the zero ideal, false otherwise.
"""
function iszero(a::MPolyQuoIdeal)
  singular_assure(a)
  zero_ideal = Singular.Ideal(base_ring(a.SI), )
  return contains(zero_ideal, a.SI)
end

function Base.show(io::IO, I::MPolyQuoIdeal)
  print(io, "MPolyQuoIdeal(")
  first = true
  for i in gens(I)
    if first
      print(io, i)
      first = false
    else
      print(io, ", ", i)
    end
  end
  print(io, ")")
end

@doc Markdown.doc"""    
    ideal(Q::MPolyQuo{T}, V::Vector{T}) where T <: MPolyElem

Given a (graded) quotient ring `Q=R/I` and a vector `V` of (homogeneous) polynomials in `R`, 
create the ideal of `Q` which is generated by the images of the entries of `V`.

    ideal(Q::MPolyQuo{T}, V::Vector{MPolyQuoElem{T}}) where T <: MPolyElem

Given a (graded) quotient ring `Q` and a vector `V` of (homogeneous) elements of `Q`, 
create the ideal of `Q` which is generated by the entries of `V`.
"""
function ideal(Q::MPolyQuo{T}, V::Vector{T}) where T <: MPolyElem
  @assert length(V) > 0
  for p in V
    Q.R == parent(p) || error("parents must match")
  end
  return MPolyQuoIdeal(Q, ideal(Q.R, V))
end
function ideal(Q::MPolyQuo{T}, V::Vector{MPolyQuoElem{T}}) where T <: MPolyElem
  @assert length(V) > 0
  for p in V
    Q == parent(p) || error("parents must match")
  end
  return MPolyQuoIdeal(Q, ideal(Q.R, map(p->p.f, V)))
end

##################################################################

function singular_ring(Rx::MPolyQuo; keep_ordering::Bool = true)
  if !isdefined(Rx, :SQR)
    groebner_assure(Rx.I)
    singular_assure(Rx.I.gb)
    Rx.SQR = Singular.create_ring_from_singular_ring(
                      Singular.libSingular.rQuotientRing(Rx.I.gb.S.ptr,
                                             base_ring(Rx.I.gb.S).ptr))
  end
  return Rx.SQR
end

parent_type(::MPolyQuoElem{S}) where S = MPolyQuo{S}
parent_type(::Type{MPolyQuoElem{S}}) where S = MPolyQuo{S}
elem_type(::MPolyQuo{S})  where S= MPolyQuoElem{S}
elem_type(::Type{MPolyQuo{S}})  where S= MPolyQuoElem{S}

canonical_unit(a::MPolyQuoElem) = one(parent(a))

parent(a::MPolyQuoElem) = a.P

function check_parent(a::MPolyQuoElem, b::MPolyQuoElem)
  a.P == b.P || error("wrong parents")
  return true
end

+(a::MPolyQuoElem, b::MPolyQuoElem) = check_parent(a, b) && MPolyQuoElem(a.f+b.f, a.P)

-(a::MPolyQuoElem, b::MPolyQuoElem) = check_parent(a, b) && MPolyQuoElem(a.f-b.f, a.P)

-(a::MPolyQuoElem) = MPolyQuoElem(-a.f, a.P)

*(a::MPolyQuoElem, b::MPolyQuoElem) = check_parent(a, b) && simplify(MPolyQuoElem(a.f*b.f, a.P))

^(a::MPolyQuoElem, b::Base.Integer) = simplify(MPolyQuoElem(Base.power_by_squaring(a.f, b), a.P))

#*(a::MPolyQuoElem, b::MPolyQuoElem) = check_parent(a, b) && MPolyQuoElem(a.f*b.f, a.P)
#
#^(a::MPolyQuoElem, b::Base.Integer) = MPolyQuoElem(Base.power_by_squaring(a.f, b), a.P)

function Oscar.mul!(a::MPolyQuoElem, b::MPolyQuoElem, c::MPolyQuoElem)
  a.f = b.f*c.f
  return a
end

function Oscar.addeq!(a::MPolyQuoElem, b::MPolyQuoElem)
  a.f += b.f
  return a
end

@doc Markdown.doc"""
    simplify(a::MPolyQuoIdeal)

Reduce `a` with regard to the modulus of the quotient ring.

    simplify!(a::MPolyQuoIdeal)

Reduce `a` with regard to the modulus of the quotient ring, and replace `a` by the reduction.

CAVEAT: The implementation proceeds by computing a Groebner basis of the quotient ideal first. This may take some time.

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"], ordering=:degrevlex);

julia> Q, q = quo(R, ideal(R, [x^3*y^2-y^3*x^2, x*y^4-x*y^2]));

julia> a = ideal(Q,[x^3*y^4-x+y, x*y+y^2*x])
MPolyQuoIdeal(x^3*y^4 - x + y, x*y^2 + x*y)

julia> simplify(a);

julia> a
MPolyQuoIdeal(x^3*y^4 - x + y, x*y^2 + x*y)

julia> simplify!(a);

julia> a
MPolyQuoIdeal(x^2*y^3 - x + y, x*y^2 + x*y)
```
"""
function simplify(a::MPolyQuoIdeal)
   R   = base_ring(a)
   oscar_assure(a)
   RI  = base_ring(a.I)
   J = R.I
   groebner_assure(J)
   singular_assure(J.gb)
   oscar_assure(a)
   singular_assure(a.I)
   red  = reduce(a.I.gens.S, J.gb.S)
   SR   = singular_ring(R)
   si   = Singular.Ideal(SR, gens(red))
   red  = MPolyQuoIdeal(R, si)
   return red
end
function simplify!(a::MPolyQuoIdeal)
    R   = base_ring(a)
    oscar_assure(a)
    RI  = base_ring(a.I)
    J = R.I
    groebner_assure(J)
    singular_assure(J.gb)
    oscar_assure(a)
    singular_assure(a.I)
    red  = reduce(a.I.gens.S, J.gb.S)
    SR   = singular_ring(R)
    a.SI = Singular.Ideal(SR, gens(red))
    a.I  = ideal(RI, RI.(gens(a.SI)))
    return a
end

@doc Markdown.doc"""
    issubset(a::MPolyQuoIdeal, b::MPolyQuoIdeal)

Return `true` if `a` is contained in `b`, `false` otherwise.

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"], ordering=:degrevlex)
(Multivariate Polynomial Ring in x, y over Rational Field, fmpq_mpoly[x, y])

julia> Q, q = quo(R, ideal(R, [x^3*y^2-y^3*x^2, x*y^4-x*y^2]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2 defined by a julia-function with inverse
)

julia> a = ideal(Q,[x^3*y^4-x+y, x*y+y^2*x])
MPolyQuoIdeal(x^3*y^4 - x + y, x*y^2 + x*y)

julia> b = ideal(Q,[x^3*y^3-x+y, x^2*y+y^2*x])
MPolyQuoIdeal(x^3*y^3 - x + y, x^2*y + x*y^2)

julia> issubset(a,b)
false

julia> issubset(b,a)
true
```
"""
function Base.issubset(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
  base_ring(a) == base_ring(b) || error("base rings must match")
  simplify!(a)
  if !(isdefined(b, :SI))
    simplify!(b)
  end
  if b.SI.isGB == false
    b.SI      = Singular.std(b.SI)
    b.SI.isGB = true
  end
  return Singular.iszero(Singular.reduce(a.SI, b.SI))
end

@doc Markdown.doc"""
    :(==)(a::MPolyQuoIdeal, b::MPolyQuoIdeal)

Return `true` if `a=b`, `false` otherwise.

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"], ordering=:degrevlex)
(Multivariate Polynomial Ring in x, y over Rational Field, fmpq_mpoly[x, y])

julia> Q, q = quo(R, ideal(R, [x^3*y^2-y^3*x^2, x*y^4-x*y^2]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2 defined by a julia-function with inverse
)

julia> a = ideal(Q,[x^3*y^4-x+y, x*y+y^2*x])
MPolyQuoIdeal(x^3*y^4 - x + y, x*y^2 + x*y)

julia> b = ideal(Q,[x^3*y^3-x+y, x^2*y+y^2*x])
MPolyQuoIdeal(x^3*y^3 - x + y, x^2*y + x*y^2)

julia> a == b
false
```
"""
function Base.:(==)(a::MPolyQuoIdeal, b::MPolyQuoIdeal)
  return issubset(a, b) && issubset(b, a)
end

@doc Markdown.doc"""
    simplify(f::MPolyQuoElem)

Reduce `f` with regard to the modulus of the quotient ring.

    simplify!(f::MPolyQuoElem)

Reduce `f` with regard to the modulus of the quotient ring, and replace `f` by the reduction.

# Examples
```jldoctest
julia> R, (x,) = PolynomialRing(QQ, ["x"]);

julia> Q, q = quo(R, ideal(R, [x^4]));

julia> f = q(x-x^6)
-x^6 + x

julia> simplify(f)
x

julia> f
-x^6 + x

julia> simplify!(f)
x

julia> f
x
```
"""
function simplify(f::MPolyQuoElem)
  R = parent(f)
  I = R.I
  groebner_assure(I)
  singular_assure(I.gb)
  Sx = base_ring(I.gb.S)
  g = f.f
  return R(I.gens.Ox(reduce(Sx(g), I.gb.S)))
end
function simplify!(f::MPolyQuoElem)
  R = parent(f)
  I = R.I
  groebner_assure(I)
  singular_assure(I.gb)
  Sx = base_ring(I.gb.S)
  g = f.f
  f.f = I.gens.Ox(reduce(Sx(g), I.gb.S))
  return f
end


@doc Markdown.doc"""
    ==(f::MPolyQuoElem, g::MPolyQuoElem)  

Return `true` if `f` is equal to `g`, `false` otherwise.

# Examples
```jldoctest
julia> R, (x,) = PolynomialRing(QQ, ["x"]);

julia> Q, q = quo(R, ideal(R, [x^4]));

julia> f = q(x-x^6)
-x^6 + x

julia> g = q(x)
x

julia> f == g
true
```
"""
function ==(f::MPolyQuoElem, g::MPolyQuoElem)
  check_parent(f, g)
  simplify!(f)
  simplify!(g)
  return f.f == g.f
end

@doc Markdown.doc"""
    quo(R::MPolyRing, I::MPolyIdeal) -> MPolyQuoRing, Map

Create the quotient ring $R/I$ and return the new
ring as well as the projection map $R\rightarrow R/I$.

    quo(R::MPolyRing, V::Vector{MPolyElem}) -> MPolyQuoRing, Map

As above, where $I\subset R$ is the ideal generated by the polynomials in $V$.

# Examples
```jldoctest
julia> R, (x, y) = PolynomialRing(QQ, ["x", "y"])
(Multivariate Polynomial Ring in x, y over Rational Field, fmpq_mpoly[x, y])

julia> Q, _ = quo(R, ideal(R, [x^2-y^3, x-y]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y defined by a julia-function with inverse
)

julia> typeof(x)
fmpq_mpoly

julia> Q, q = quo(R, ideal(R, [x^2-y^3, x-y]))
(Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y, Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y defined by a julia-function with inverse
)

julia> q
Map from
Multivariate Polynomial Ring in x, y over Rational Field to Quotient of Multivariate Polynomial Ring in x, y over Rational Field by ideal generated by: x^2 - y^3, x - y defined by a julia-function with inverse

julia> q(x)
x

julia> typeof(q(x))
MPolyQuoElem{fmpq_mpoly}

julia> S, (x, y, z) = GradedPolynomialRing(QQ, ["x", "y", "z"]);

julia> P, p = quo(S, ideal(S, [x^2*z-y^3, x-y]))
(Quotient of Multivariate Polynomial Ring in x, y, z over Rational Field graded by
  x -> [1]
  y -> [1]
  z -> [1] by ideal generated by: x^2*z - y^3, x - y, Map from
Multivariate Polynomial Ring in x, y, z over Rational Field graded by
  x -> [1]
  y -> [1]
  z -> [1] to Quotient of Multivariate Polynomial Ring in x, y, z over Rational Field graded by
  x -> [1]
  y -> [1]
  z -> [1] by ideal generated by: x^2*z - y^3, x - y defined by a julia-function with inverse
)
```
"""
function quo(R::MPolyRing, I::MPolyIdeal) 
  q = MPolyQuo(R, I)
  function im(a::MPolyElem)
    return MPolyQuoElem(a, q)
  end
  function pr(a::MPolyQuoElem)
    return a.f
  end
  return q, MapFromFunc(im, pr, R, q)
end
function quo(R::MPolyRing, I::Vector{<:MPolyElem})
  return quo(R, ideal(I))
end

function quo(R::MPolyRing, f::MPolyElem...)
  return quo(R, ideal(collect(f)))
end

lift(a::MPolyQuoElem) = a.f

(Q::MPolyQuo)() = MPolyQuoElem(Q.R(), Q)

function (Q::MPolyQuo)(a::MPolyQuoElem)
   @assert parent(a) == Q
   return a
end

function (Q::MPolyQuo)(a::MPolyElem) 
   @assert parent(a) == Q.R
   return MPolyQuoElem(Q.R(a), Q)
end

function (Q::MPolyQuo)(a::Singular.spoly)
   @assert singular_ring(Q) == parent(a)
   return MPolyQuoElem(Q.R(a), Q)
end


function (S::Singular.PolyRing)(a::MPolyQuoElem)
   Q = parent(a)
   @assert singular_ring(Q) == S
   return S(a.f)
end

(Q::MPolyQuo)(a) = MPolyQuoElem(Q.R(a), Q)

zero(Q::MPolyQuo) = Q(0)
one(Q::MPolyQuo) = Q(1)

function isinvertible_with_inverse(a::MPolyQuoElem)
  Q = parent(a)
  I = Q.I
  if isdefined(I, :gb)
    J = I.gb.O
  else
    J = gens(I)
  end
  J = vcat(J, [a.f])
  H, T = groebner_basis_with_transform(ideal(J))
  if 1 in H
    @assert nrows(T) == 1
    return true, Q(T[1, end])
  end
  return false, a
end

isunit(a::MPolyQuoElem) = isinvertible_with_inverse(a)[1]
function inv(a::MPolyQuoElem)
  fl, b = isinvertible_with_inverse(a)
  fl || error("Element not invertible")
  return b
end

"""
Tries to write the generators of `M` as linear combinations of generators in `SM`.
Extremely low level, might migrate to Singular.jl and be hidd...
"""
function lift(M::Singular.sideal, SM::Singular.sideal)
  R = base_ring(M)
  ptr,rest_ptr = Singular.libSingular.id_Lift(M.ptr, SM.ptr, R.ptr)
  return Singular.Module(R, ptr), Singular.Module(R,rest_ptr)
end

"""
Converts a sparse-Singular vector of polynomials to an Oscar sparse row.
"""
function sparse_row(R::MPolyRing, M::Singular.svector{<:Singular.spoly})
  v = Dict{Int, MPolyBuildCtx}()
  for (i, e, c) = M
    if !haskey(v, i)
      v[i] = MPolyBuildCtx(R)
    end
    push_term!(v[i], base_ring(R)(c), e)
  end
  sparse_row(R, [(k,finish(v)) for (k,v) = v])
end

"""
Converts a sparse-Singular vector of polynomials to an Oscar sparse row.
Collect only the column indices in `U`.
"""
function sparse_row(R::MPolyRing, M::Singular.svector{<:Singular.spoly}, U::UnitRange)
  v = Dict{Int, MPolyBuildCtx}()
  for (i, e, c) = M
    (i in U) || continue
    if !haskey(v, i)
      v[i] = MPolyBuildCtx(R)
    end
    push_term!(v[i], base_ring(R)(c), e)
  end
  sparse_row(R, [(k,finish(v)) for (k,v) = v])
end

"""
Converts the sparse-Singular matrix (`Module`) row by row to an Oscar sparse-matrix.
Only the row indeces (generators) in `V` and the column indeces in `U` are converted.
"""
function sparse_matrix(R::MPolyRing, M::Singular.Module, V::UnitRange, U::UnitRange)
  S = sparse_matrix(R)
  for g = 1:Singular.ngens(M)
    (g in V) || continue
    push!(S, sparse_row(R, M[g], U))
  end
  return S
end

"""
Converts the sparse-Singular matrix (`Module`) row by row to an Oscar sparse-matrix.
"""
function sparse_matrix(R::MPolyRing, M::Singular.Module)
  S = sparse_matrix(R)
  for g = 1:Singular.ngens(M)
    push!(S, sparse_row(R, M[g]))
  end
  return S
end

"""
Converts the sparse-Singular matrix (`Module`) row by row to an Oscar dense-matrix.
"""
function matrix(R::MPolyRing, M::Singular.Module)
  return matrix(sparse_matrix(R, M))
end

function divides(a::MPolyQuoElem, b::MPolyQuoElem)
  check_parent(a, b)
  simplify!(a) #not neccessary
  simplify!(b) #not neccessary
  iszero(b) && error("cannot divide by zero")

  Q = parent(a)
  I = Q.I
  if isdefined(I, :gb)
    J = I.gb.O
  else
    J = gens(I)
  end

  BS = BiPolyArray([a.f], keep_ordering = false)
  singular_assure(BS)

  J = vcat(J, [b.f])
  BJ = BiPolyArray(J, keep_ordering = false)
  singular_assure(BJ)

  s, = Singular.lift(BJ.S, BS.S)
  if Singular.ngens(s) < 1 || iszero(s[1])
    return false, a
  end
  return true, Q(sparse_matrix(base_ring(Q), s, 1:1, length(J):length(J))[1, length(J)])
end

#TODO: find a more descriptive, meaningful name
function _kbase(Q::MPolyQuo)
  I = Q.I
  groebner_assure(I)
  singular_assure(I.gb)
  s = Singular.kbase(I.gb.S)
  if iszero(s)
    error("ideal was no zero-dimensional")
  end
  return [Q.R(x) for x = gens(s)]
end

#TODO: the reverse map...
# problem: the "canonical" reps are not the monomials.
function vector_space(K::AbstractAlgebra.Field, Q::MPolyQuo)
  R = Q.R
  @assert K == base_ring(R)
  l = _kbase(Q)
  V = free_module(K, length(l))
  function im(a::Generic.FreeModuleElem)
    @assert parent(a) == V
    b = R(0)
    for i=1:length(l)
      c = a[i]
      if !iszero(c)
        b += c*l[i]
      end
    end
    return Q(b)
  end
  return V, MapFromFunc(im, V, Q)
end

# To fix printing of fraction fields of MPolyQuo
function AbstractAlgebra.expressify(a::AbstractAlgebra.Generic.Frac{T};
                                    context = nothing) where {T <: MPolyQuoElem}
  n = numerator(a, false)
  d = denominator(a, false)
  if isone(d)
    return expressify(n, context = context)
  else
    return Expr(:call, ://, expressify(n, context = context),
                            expressify(d, context = context))
  end
end

################################################################################
#
#  Graded functionality
#
################################################################################

function degree(a::MPolyQuoElem{<:MPolyElem_dec})
  simplify!(a)
  return degree(a.f)
end

isfiltered(q::MPolyQuo) = isfiltered(q.R)
isgraded(q::MPolyQuo) = isgraded(q.R)

function homogeneous_component(a::MPolyQuoElem{<:MPolyElem_dec}, d::GrpAbFinGenElem)
  simplify!(a)
  return homogeneous_component(a.f, d)
end

function homogeneous_components(a::MPolyQuoElem{<:MPolyElem_dec})
  simplify!(a)
  return homogeneous_components(a.f)
end

function ishomogeneous(a::MPolyQuoElem{<:MPolyElem_dec})
  simplify!(a)
  return ishomogeneous(a.f)
end

decoration(q::MPolyQuo{<:MPolyElem_dec}) = decoration(q.R)

function hash(w::MPolyQuoElem, u::UInt)
  simplify!(w)
  return hash(w.f, u)
end

function homogeneous_component(W::MPolyQuo{<:MPolyElem_dec}, d::GrpAbFinGenElem)
  #TODO: lazy: ie. no enumeration of points
  #      aparently it is possible to get the number of points faster than the points
  D = parent(d)
  @assert D == decoration(W)
  R = base_ring(W)
  I = modulus(W)

  H, mH = homogeneous_component(R, d)
  B = Set{elem_type(W)}()
  for h = basis(H)
    b = W(mH(h))
    if !iszero(b)
      push!(B, b)
    end
  end
  B = [x for x = B]

  M, h = vector_space(base_ring(R), B, target = W)
  Hecke.set_special(M, :show => show_homo_comp, :data => (W, d))
  return M, h
end

@doc Markdown.doc"""
    dim(a::MPolyQuoIdeal)

Return the Krull dimension of `a`.

# Examples
```jldoctest
julia> R, (x, y, z) = PolynomialRing(QQ, ["x", "y", "z"], ordering=:degrevlex)
(Multivariate Polynomial Ring in x, y, z over Rational Field, fmpq_mpoly[x, y, z])

julia> Q, q = quo(R, ideal(R, [x^3*y^2-y^3*x^2, x*y^4-x*y^2]))
(Quotient of Multivariate Polynomial Ring in x, y, z over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2, Map from
Multivariate Polynomial Ring in x, y, z over Rational Field to Quotient of Multivariate Polynomial Ring in x, y, z over Rational Field by ideal generated by: x^3*y^2 - x^2*y^3, x*y^4 - x*y^2 defined by a julia-function with inverse
)

julia> a = ideal(Q,[x^3*y^4-x+y, x*y+y^2*x])
MPolyQuoIdeal(x^3*y^4 - x + y, x*y^2 + x*y)

julia> dim(a)
1
```
"""
function dim(a::MPolyQuoIdeal)
  if a.dim > -1
    return a.dim
  end
  singular_assure(a)
	a.SI			=	Singular.std(a.SI)
	a.SI.isGB	=	true
  a.dim 		= Singular.dimension(a.SI)
  return a.dim
end
#######################################################
