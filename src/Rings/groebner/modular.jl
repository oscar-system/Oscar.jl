# modular gröbner basis techniques using Singular
@doc raw"""
    groebner_basis_modular(I::MPolyIdeal{QQMPolyRingElem}; ordering::MonomialOrdering = default_ordering(base_ring(I)), certify::Bool = false)

Compute the reduced Gröbner basis of `I` w.r.t. `ordering` using a
multi-modular strategy. When `certify = true` the result is for sure a Gröbner basis of `I`, else
only with a high probability.

```jldoctest
julia> R, (x, y, z) = polynomial_ring(QQ, [:x,:y,:z]);

julia> I = ideal(R, [x^2+1209, x*y + 3279*y^2])
Ideal generated by
  x^2 + 1209
  x*y + 3279*y^2

julia> groebner_basis_modular(I)
Gröbner basis with elements
  1: x*y + 3279*y^2
  2: x^2 + 1209
  3: y^3 + 403//3583947*y
with respect to the ordering
  degrevlex([x, y, z])
```
"""
function groebner_basis_modular(I::MPolyIdeal{QQMPolyRingElem}; ordering::MonomialOrdering = default_ordering(base_ring(I)),
                                certify::Bool = false)
  SG = Singular.LibModstd.modStd(Oscar.singular_generators(I, ordering), Int(certify))

  G = IdealGens(base_ring(I), SG)
  G.isGB = true
  G.ord = ordering
  if isdefined(G, :S)
     G.S.isGB  = true
  end
  I.gb[ordering] = G
  I.gb[ordering].isGB = true
  return I.gb[ordering]
end


function induce_rational_reconstruction(f::ZZMPolyRingElem, d::ZZRingElem; parent = 1)
  g = MPolyBuildCtx(parent)
  for (c, v) in zip(AbstractAlgebra.coefficients(f), AbstractAlgebra.exponent_vectors(f))
    fl, r, s = Hecke.rational_reconstruction(c, d)
    fl ? push_term!(g, r//s, v) : push_term!(g, c, v)
  end
  return finish(g)
end

function _certify_modular_groebner_basis(I::MPolyIdeal, ordering::MonomialOrdering)
  @req haskey(I.gb, ordering) "There exists no standard basis w.r.t. the given ordering."
  singular_generators(I.gb[ordering])
  SR = I.gb[ordering].gensBiPolyArray.Sx
  SG = I.gb[ordering].gensBiPolyArray.S

  #= test if I is included in <G> =#
  for f in I.gens
    if Singular.reduce(SR(f), SG) != 0
      return false
    end
  end

  #= test if G is a standard basis of <G> w.r.t. ordering =#
  return is_standard_basis(I.gb[ordering], ordering=ordering)
end
